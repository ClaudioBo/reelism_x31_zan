// Splot - Visual FX

ACTOR PuffBase
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	+PUFFONACTORS
	+CLIENTSIDEONLY
	RenderStyle Translucent
	Alpha 0.5
	Mass 5
	States
	{
	Spawn:
		PUFF A 0
		PUFF A 0 A_PlaySound("weapons/ric")
		PUFF A 4 bright
		PUFF BCD 4
		stop
	}
}

ACTOR BloodShred replaces Blood
{
	Mass 1
	Radius 1
	Height 1
	Scale 0.7
	Alpha 1
	Gravity .125
	+NOBLOCKMAP
	+FLOORCLIP
	+NOTELEPORT
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPRY A 0 
		SPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		SPRY A 0 A_PlaySound("human/damage",CHAN_AUTO)
		SPRY AB 2
		SPRY C 2 A_SpawnDebris("BloodSpurt",1)
		SPRY DEFG 2
		Stop
	BloodMoney:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("GoldCoin",0,0,0,random(4,16),0,random(8,16),random(0,360),SXF_ABSOLUTEANGLE)
		stop
	}
}

ACTOR FuckYouHexenYouShit : BloodShred replaces BloodSplatter { }

ACTOR BloodShredSilent : BloodShred
{
	States
	{
	Spawn:
		SPRY A 0 
		SPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		SPRY AB 2
		SPRY C 2 A_SpawnDebris("BloodSpurt",1)
		SPRY DEFG 2
		Stop
	}
}

ACTOR BloodSpurt
{
	Health 4
	radius 3
	height 6
	speed .1
	Scale .2
	Mass 0
	BounceType Doom
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	+NOBLOCKMAP
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPRY A 0 
		SPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		BBLD ABCDABCDABCDABCD 1
		BBLD AB 1 A_SetTranslucent(.8)
		BBLD CD 1 A_SetTranslucent(.6)
		BBLD AB 1 A_SetTranslucent(.4)
		BBLD CD 1 A_SetTranslucent(.2)
		Stop
	BloodMoney:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("GoldCoin",0,0,0,random(4,16),0,random(8,16),random(0,360),SXF_ABSOLUTEANGLE)
		stop
	}
}

ACTOR HugeBloodShred : BloodShred
{
	Scale 2
	States
	{
	Spawn:
		SPRY A 0 
		SPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		SPRY AB 2
		SPRY C 2 A_SpawnDebris("BloodSpurt",1)
		SPRY DEFG 2
		Stop
	}
}

ACTOR HugeBloodSpurt : BloodSpurt
{
	Health 8
	Scale 1
	States
	{
	Spawn:
		SPRY BCDBCDBCDBCD 1
		SPRY BC 1 A_SetTranslucent(.8)
		SPRY CD 1 A_SetTranslucent(.6)
		SPRY BC 1 A_SetTranslucent(.4)
		SPRY CD 1 A_SetTranslucent(.2)
		Stop
	}
}

// Green for Barons and Barrels
ACTOR GreenBloodShred : BloodShred
{
	States
	{
	Spawn:
		GPRY A 0
		GPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		GPRY A 0 A_PlaySound("human/damage",CHAN_AUTO)
		GPRY AB 2
		GPRY C 2 A_SpawnDebris("GreenBloodSpurt",1)
		GPRY DEFG 2
		Stop
	}
}
ACTOR GreenBloodShredSilent : GreenBloodShred
{
	States
	{
	Spawn:
		GPRY A 0 
		GPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		GPRY AB 2
		GPRY C 2 A_SpawnDebris("BloodSpurt",1)
		GPRY DEFG 2
		Stop
	}
}

ACTOR GreenBloodSpurt : BloodSpurt
{
	States
	{
	Spawn:
		GBLD ABCDABCDABCDABCD 1
		GBLD AB 1 A_SetTranslucent(.8)
		GBLD CD 1 A_SetTranslucent(.6)
		GBLD AB 1 A_SetTranslucent(.4)
		GBLD CD 1 A_SetTranslucent(.2)
		Stop
	}
}

ACTOR GreenHugeBloodShred : GreenBloodShred
{
	scale 2
	States
	{
	Spawn:
		GPRY A 0 
		GPRY A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==18,"BloodMoney")
		GPRY AB 2
		GPRY C 2 A_SpawnDebris("BloodSpurt",1)
		GPRY DEFG 2
		Stop
	}
}
ACTOR GreenHugeBloodSpurt : GreenBloodSpurt
{
	Health 8
	Scale 1
	States
	{
	Spawn:
		GPRY BCDBCDBCDBCD 1
		GPRY BC 1 A_SetTranslucent(.8)
		GPRY CD 1 A_SetTranslucent(.6)
		GPRY BC 1 A_SetTranslucent(.4)
		GPRY CD 1 A_SetTranslucent(.2)
		Stop
	}
}

// Particles
Actor LandingPart
{
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 AAAAAAA 0 A_SpawnItemEx("LandPart",0,0,0,(0.1)*Random(35,40),0,(0.1)*Random(-5,5),Random(-128,128))
		stop
	}
}

Actor DespawnCloudPart : LandingPart
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_PlaySound("items/despawn")
		Goto Super::Spawn+1
	}
}

Actor DespawnPart
{
	Height 1
	Radius 1
	//+Missile
	+NoGravity
	+NoBlockMap
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	RenderStyle Add
	Scale 0.4
	Alpha 0.65
	States
	{
	Spawn:
		STEM A 0
		STEM A 0 A_Recoil(-4)
		STEM A 1 A_SetTranslucent(0.6,1)
		STEM B 1 A_SetTranslucent(0.55,1)
		STEM C 1 A_SetTranslucent(0.5,1)
		STEM D 1 A_SetTranslucent(0.45,1)
		STEM E 1 A_SetTranslucent(0.4,1)
		STEM F 1 A_SetTranslucent(0.35,1)
		STEM G 1 A_SetTranslucent(0.3,1)
		STEM H 1 A_SetTranslucent(0.25,1)
		STEM I 1 A_SetTranslucent(0.2,1)
		STEM J 1 A_SetTranslucent(0.15,1)
		STEM K 1 A_SetTranslucent(0.1,1)
		STEM L 1 A_SetTranslucent(0.05,1)
		Stop
	}
}

Actor LandPart : DespawnPart
{
	-SOLID
	Alpha 0.33
	States
	{
	Spawn:
		STEM A 0
		STEM A 0 A_Recoil(-2)
		Goto Super::Spawn+2
	}
}

Actor MissilePart : DespawnPart
{
	Scale 0.3
	Alpha 0.33
	States
	{
	Spawn:
		Goto Super::Spawn+2
	}
}

ACTOR MetalSpark
{
	Mass 1
	Radius 1
	Height 1
	Scale 0.7
	Gravity .125
	+NOBLOCKMAP
	+FLOORCLIP
	+NOTELEPORT
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPRK A 0
		SPRK A 2 A_PlaySound("tank/hit",CHAN_AUTO)
		SPRK B 2
		SPRK C 2 A_SpawnDebris("MetalSpark2",1)
		SPRK D 2
		Stop
	}
}

ACTOR MetalSpark2 : MetalSpark
{
	Gravity 1
	Health 4
	radius 3
	height 6
	speed .1
	Scale .2
	Mass 0
	BounceType Doom
	+DONTSPLASH
	States
	{
	Spawn:
		SPRK ABCDABCDABCDABCD 1
		SPRK AB 1 A_SetTranslucent(.8)
		SPRK CD 1 A_SetTranslucent(.6)
		SPRK AB 1 A_SetTranslucent(.4)
		SPRK CD 1 A_SetTranslucent(.2)
		Stop
	}
}

//==================================//
//Explosions by KeksDose			//**********//
//==================================//

//*****Smoke*****//

//*****Black*****//

Actor Particle_Smoke_Normal1
{
	+DONTSPLASH
	Radius 4
	Height 8
	+NoBlockmap
	+NoGravity
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	Scale 0.75
	States
	{
	Spawn:
		SMSC A 0 A_FadeOut(0.025)
		SMSC A 4 A_JumpIf(CallACS("SmokeToggleCheck")==0,"Death")
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

Actor Particle_Smoke_Normal2 : Particle_Smoke_Normal1{Scale 1.00}
Actor Particle_Smoke_Normal3 : Particle_Smoke_Normal1{Scale 1.25}
Actor Particle_Smoke_Normal4 : Particle_Smoke_Normal1{Scale 1.50}
Actor Particle_Smoke_Normal5 : Particle_Smoke_Normal1{Scale 0.50}
Actor Particle_Smoke_Normal6 : Particle_Smoke_Normal1{Scale 0.15}

//*****Explosions*****//

//*****Normal Fire Colors*****//

Actor Particle_Explosion_Normal1 : Particle_Smoke_Normal1
{
	RenderStyle Add
	//+FORCEXYBILLBOARD
	Scale 0.75
	States
	{
	Spawn:
		EXNO A 2 Bright A_FadeOut(0.25)
		Loop
	}
}

Actor Particle_Explosion_Normal2 : Particle_Explosion_Normal1{Scale 1.00}
Actor Particle_Explosion_Normal3 : Particle_Explosion_Normal1{Scale 1.25}
Actor Particle_Explosion_Normal4 : Particle_Explosion_Normal1{Scale 1.50}

//*****Normal*****//

Actor Spawner_Explosion_Normal1 : Particle_Smoke_Normal1
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 AA 0 A_SpawnItemEx("Particle_Explosion_Normal1")
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("Particle_Smoke_Normal1",Random(-16,16),Random(-16,16),Random(-16,16),Random(-1,1),Random(-1,1),Random(-1,1))
		Stop
	}
}

Actor Spawner_Explosion_Normal2 : Particle_Smoke_Normal1
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 AA 0 A_SpawnItemEx("Particle_Explosion_Normal2")
		TNT1 AAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Particle_Smoke_Normal2",Random(-20,20),Random(-20,20),Random(-20,20),Random(-1,1),Random(-1,1),Random(-1,1))
		Stop
	}
}

Actor Spawner_Explosion_Normal3 : Particle_Smoke_Normal1
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 AA 0 A_SpawnItemEx("Particle_Explosion_Normal3")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Particle_Smoke_Normal3",Random(-30,30),Random(-30,30),Random(-30,30),Random(-1,1),Random(-1,1),Random(-1,1))
		Stop
	}
}

Actor Spawner_Explosion_Normal4 : Particle_Smoke_Normal1
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 AA 0 A_SpawnItemEx("Particle_Explosion_Normal4")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Particle_Smoke_Normal4",Random(-40,40),Random(-40,40),Random(-40,40),Random(-1,1),Random(-1,1),Random(-1,1))
		Stop
	}
}

//*****End*****//

// Weather FX

actor RainDrop
{
	height 1
	radius 1
	scale 0.5
	+CORPSE
	+DONTSPLASH
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		RAIN A 1
		Loop
	Crash:
		RAIN B 3
		Stop
	}
}

actor RainCloud 30504
{
	height 1
	radius 1
	+SPAWNCEILING
	+NOGRAVITY
	+RANDOMIZE
	+CLIENTSIDEONLY
	//$Category Reelism - Ambience
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallACS("SmokeToggleCheck")==0,2)
		TNT1 A 0 A_SpawnItemEx("RainDrop",random(-128,128),random(-128,128))
		TNT1 A 3
		Loop
	}
}

ACTOR AmbSound
{
	height 1
	radius 32
	+NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	Silent:
		TNT1 A -1 A_StopSound(CHAN_BODY) //Can be reset with SetActorState
		Stop
	}
}

actor RainSound : AmbSound 30505
{
	//$Category Reelism - Ambience
	states
	{
	Spawn:
		TNT1 A 1 A_PlaySound("ambient/rainloop",CHAN_BODY,1,1)
		loop
	}
}

actor TeleportSound : AmbSound 30506
{
	//$Category Reelism - Ambience
	states
	{
	Spawn:
		TNT1 A 1 A_PlaySound("ambience/teleporter",CHAN_BODY,1,1)
		loop
	}
}

actor WaterSound : AmbSound 30507
{
	//$Category Reelism - Ambience
	states
	{
	Spawn:
		TNT1 A 1 A_PlaySound("ambience/water",CHAN_BODY,1,1)
		loop
	}
}

actor FountainSound : AmbSound 30508
{
	//$Category Reelism - Ambience
	states
	{
	Spawn:
		TNT1 A 1 A_PlaySound("ambience/fountain",CHAN_BODY,1,1)
		loop
	}
}

// Shells and Casings
// Blatantly stolen from Brutal Doom. :dealwithit:

ACTOR BulletCasingSpawn
{
	Speed 20
	PROJECTILE
	+NOCLIP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_CustomMissile("EmptyBrass",-5,0,random(-80,-100),2,random(45,80))
		Stop
	}
}

ACTOR ShotCaseSpawn : BulletCasingSpawn
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_CustomMissile("ShotgunCasing",0,0,random(-80,-100),2,random(40,60))
		Stop
	}
}

Actor SSGCaseSpawner : ShotcaseSpawn
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 Thing_ChangeTID(0,390)
	Death:
		TNT1 A 1 A_CustomMissile("ShotgunCasing",0,0,random(80,100),2,random(50,70))
		Stop
	}
}

actor EmptyBrass
{
	BounceType Doom
	PROJECTILE
	-NOGRAVITY
	-NOBLOCKMAP
	-NOTELEPORT
	+dontsplash
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	Radius 2
	SeeSound "weapons/casing1"
	Speed 6
	scale 0.2
	BounceFactor 0.4
	States
	{
	Spawn:
		PBUL A 0
		PBUL A 0 A_Jump(255,"Fly1","Fly2","Fly3","Fly4","Fly5","Fly6")
		goto Fly1
	Bounce:
		goto Spawn
	Fly1:
		"####" ABCDEFGH 5
		loop
	Fly2:
		"####" ABCDEFGH 3
		loop
	Fly3:
		"####" ABCDEFGH 2
		loop
	Fly4:
		"####" HGFEDCBA 5
		loop
	Fly5:
		"####" HGFEDCBA 3
		loop
	Fly6:
		"####" HGFEDCBA 2
		loop
	Death:
		"####" A 0 A_Jump(255,"Stop1","Stop2","Stop3","Stop4")
		goto Stop1
	Stop1:
		"####" B 250 A_CheckSight("Vanish")
		loop
	Stop2:
		"####" D 250 A_CheckSight("Vanish")
		loop
	Stop3:
		"####" F 250 A_CheckSight("Vanish")
		loop
	Stop4:
		"####" H 250 A_CheckSight("Vanish")
		loop
	Vanish:
		TNT1 A 1
		Stop
	}
}

actor ShotgunCasing : EmptyBrass
{
	SeeSound "weapons/casing2"
	States
	{
	Spawn:
		PSHE A 0
		PSHE A 0 A_Jump(255,"Fly1","Fly2","Fly3","Fly4","Fly5","Fly6")
		goto Fly1
	Bounce:
		goto Spawn
	}
}

// Warper
actor TelefogParticle
{
	BounceType Doom
	PROJECTILE
	-NOGRAVITY
	-NOBLOCKMAP
	-NOTELEPORT
	+dontsplash
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	Radius 2
	Speed 6
	scale 0.2
	BounceFactor 0.4
	States
	{
	Spawn:
		TFOG ABCDEFGHIJ 2 Bright
		stop
	}
}

ACTOR NewTeleportFog : TeleportFog replaces TeleportFog
{
	+NOBLOCKMAP
	+NOTELEPORT
	+NOGRAVITY
	RenderStyle Add
	//+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAA 0 A_CustomMissile("TelefogParticle",random(4,16),0,random(0,360),2,random(4,32))
		TFOG ABCDEFGHIJ 2 Bright
		Stop
	}
}

ACTOR MorphOnEffect : NewTeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("morph/on")
		Goto Super::Spawn
	}
}

ACTOR MorphOffEffect : NewTeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("morph/off")
		Goto Super::Spawn
	}
}

ACTOR MorphOnTankEffect : NewTeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("morph/ontank")
		Goto Super::Spawn
	}
}

ACTOR MorphOnDogEffect : NewTeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("morph/dogon")
		Goto Super::Spawn
	}
}

ACTOR MorphOnCybEffect : NewTeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("morph/oncyb")
		Goto Super::Spawn
	}
}

ACTOR MorphOffDogEffect : NewTeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("morph/dogoff")
		Goto Super::Spawn
	}
}

////////////////////////////
// SPARKLE SPAWNERS ////////
// for REALM667	 ////////
// by Tormentor667  ////////
////////////////////////////


///////////////////////////////// DOOM VARIANT /////////////////////////////////////////////

Actor BaseSparkle
{
	+Missile
	+NoGravity
	+NoBlockmap
	+NoInteraction
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.6
	Scale 0.1
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		SKFX A 0
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.2)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.3)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.4)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.5)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.6)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.7)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.8)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(0.9)
		SKFX A 1 Bright A_FadeOut(0.1)
		SKFX A 1 Bright A_SetScale(1.0)
		SKFX A 1 Bright A_FadeOut(0.1)
		Stop
	}
}

actor GreenSparkleSpawner
{
	+MOVEWITHSECTOR
	+NoGravity
	+NoInteraction
	states
	{
	Spawn:
		TNT1 A 1 NODELAY A_SpawnItemEx("BaseSparkle",random(-16,16),random(-16,16),random(0,8),0,0,0,0,SXF_TRANSFERTRANSLATION,128)
		Loop
	}
}

actor BlueSparkleSpawner : GreenSparkleSpawner
{
	translation "112:127=196:207"
}
actor WhiteSparkleSpawner : GreenSparkleSpawner
{
	translation "112:127=80:111"
}
actor RedSparkleSpawner : GreenSparkleSpawner
{
	translation "112:127=176:191"
}
actor OrangeSparkleSpawner: GreenSparkleSpawner
{
	translation "112:127=214:223"
}

// Duke 3D Gibs - Code stolen from Samsara
// Thanks, TerminusEst13!

actor DukeGibs1
{
	Radius 1
	Height 1
	Speed 0
	Projectile
	-NOGRAVITY
	-SOLID
	+MOVEWITHSECTOR
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOTELEPORT
	Gravity 0.65
	Health 2
	scale 0.5
	States
	{
	Spawn:
		DKG1 ABCD 5
		loop
	Death:
		DKG6 CDEFGH 5 A_Gravity
		stop
	}
}

actor DukeGibs2 : DukeGibs1
{
	Health 2
	States
	{
	Spawn:
		DKG2 ABCD 5
		loop
	Death:
		DKG6 CDEFGH 5 A_Gravity
		stop
	}
}

actor DukeGibs3 : DukeGibs1
{
	Health 4
	States
	{
	Spawn:
		DKG3 ABCD 5
		loop
	Death:
		DKG6 CDEFGH 5 A_Gravity
		stop
	}
}
actor DukeGibs4 : DukeGibs1
{
	States
	{
	Spawn:
		DKG4 ABCD 5
		loop
	Death:
		DKG6 CDEFGH 5 A_Gravity
		stop
	}
}

actor DukeGibs5 : DukeGibs1
{
	Health 2
	States
	{
	Spawn:
		DKG5 ABCD 5
		loop
	Death:
		DKG6 CDEFGH 5 A_Gravity
		stop
	}
}

actor DukeGibs6 : DukeGibs1
{
	Health 7
	States
	{
	Spawn:
		DKG6 AB 5
		loop
	Death:
		DKG6 CDEFGH 5 A_Gravity
		stop
	}
}

// x3 grenade shit

ACTOR ConcExplosion
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	+PUFFONACTORS
	+CLIENTSIDEONLY
	Alpha 0.5
	scale 2
	States
	{
	Spawn:
		COBM A 0 bright
		COBM A 4 bright
		COBM B 2 bright 
		TNT1 A 1
		COBM B 1
		TNT1 A 1
		COBM B 1
		stop
	}
}

ACTOR IceExplosion
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	+PUFFONACTORS
	+CLIENTSIDEONLY
	scale 2
	States
	{
	Spawn:
		NFZS A 0 bright
		NFZS A 2 bright
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("Particle_Smoke_Normal3",Random(-30,30),Random(-30,30),Random(-30,30),Random(-1,1),Random(-1,1),Random(-1,1))
		stop
	}
}