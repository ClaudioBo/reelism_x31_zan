// Splot - Throwables

// Grenades
actor GrenadePickup : CustomInventory
{
	inventory.pickupmessage "$GOTGRENADES"
	Tag "$TAGGRENADES"
	inventory.icon "INVGREN"
	inventory.pickupsound "weapons/grenadebounce"
	inventory.amount 3
	inventory.maxamount 12
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.INVBAR
	scale 0.45
	states
	{
	Spawn:
		PUGR A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Use:
		TNT1 A 0 A_FireCustomMissile("GrenadeProj",0,0)
		stop
	}
}

actor GrenadeProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 10
	Damage 20
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	//+FORCEXYBILLBOARD
	BounceType "Doom"
	SeeSound "weapons/grenadethrow"
	BounceSound "weapons/grenadebounce"
	DeathSound "weapons/explode"
	Obituary "$OB_MPGRENADE"
	scale 0.3
	damagetype Explosive
	ReactionTime 60	//Just over five seconds
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,16,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Active
	Active:
		GREN ABCDEFGH 3 A_CountDown
		loop
	Death:
		TNT1 A 0 A_Explode
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItem("Spawner_Explosion_Normal1")
		stop
	}
}

actor BirthdayPartyBomb
{
	Radius 1
	Height 1
	Speed 10
	//Damage 20
	PROJECTILE
	DeathSound "weapons/explode"
	Obituary "$OB_BIRTHDAYPARTY"
	scale 0.3
	damagetype Explosive
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Explode(20,128,1)
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_PlaySound("weapons/explode",CHAN_VOICE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItem("Spawner_Explosion_Normal1")
		stop
	}
}

// Flechette
actor FlechettePickup : CustomInventory
{
	inventory.pickupmessage "$GOTFLECHETTE"
	Tag "$TAGFLECHETTE"
	inventory.icon "ARTIPSBG"
	inventory.pickupsound "weapons/flechette/get"
	inventory.amount 3
	inventory.maxamount 12
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.INVBAR
	states
	{
	Spawn:
		PSBG A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Use:
		TNT1 A 0 A_FireCustomMissile("Flechette",0,0)
		stop
	}
}

ACTOR Flechette
{
	+CANBOUNCEWATER
	Speed 12
	Radius 8
	Height 10
	//Damage 20
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	//+FORCEXYBILLBOARD
	BounceType "Doom"
	SeeSound "weapons/flechette/throw"
	BounceSound "weapons/flechette/bounce"
	DeathSound "weapons/flechette/explode"
	Obituary "$OB_MPGRENADE"
	ReactionTime 45	//Just over five seconds
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,32,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Active
	Active:
		THRW ABCDEF 3 A_CountDown
		loop
	Death:
		CFCF Q 4 Bright A_NoGravity
		CFCF R 3 Bright A_Scream
		CFCF S 4 Bright A_SpawnItem("PoisonCloud", 0, 28)
		CFCF T 3 Bright
		CFCF U 4 Bright
		CFCF W 3 Bright
		CFCF X 4 Bright
		CFCF Z 3 Bright
		Stop
	}
}

// Conc it Up
actor ConcPickup : CustomInventory
{
	inventory.pickupmessage "$GOTCONC"
	Tag "$TAGCONC"
	inventory.icon "INVCONC"
	inventory.pickupsound "weapons/conc/pickup"
	inventory.amount 3
	inventory.maxamount 12
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.INVBAR
	scale 0.45
	states
	{
	Spawn:
		CONP A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Use:
		TNT1 A 0 A_FireCustomMissile("ConcProj",0,0)
		stop
	}
}

actor ConcProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 15
	Damage (20)
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	//+FORCEXYBILLBOARD
	BounceType "Doom"
	SeeSound "weapons/conc/throw"
	BounceSound "weapons/conc/bounce"
	DeathSound "weapons/conc/boom"
	Obituary "$OB_MPGRENADE"
	scale 0.2
	damagetype Explosive
	ReactionTime 60	//Just over five seconds
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,24,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Active
	Active:
		QGRN CDEFGHIJKLMN 3 A_CountDown
		loop
	Death:
		TNT1 A 0 A_Blast(0,255,500,50,"LandingPart","powerups/repulsor")
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItem("ConcExplosion")
		stop
	}
}

// Grenades
actor IceBombPickup : CustomInventory
{
	inventory.pickupmessage "$GOTICEBOMB"
	Tag "$TAGICEBOMB"
	inventory.icon "INVICEG"
	inventory.pickupsound "weapons/icebomb/get"
	inventory.amount 3
	inventory.maxamount 12
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.INVBAR
	scale 0.45
	states
	{
	Spawn:
		ICEG Z 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Use:
		TNT1 A 0 A_FireCustomMissile("IceBombProj",0,0)
		stop
	}
}

actor IceBombProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 15
	Damage 20
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	//+FORCEXYBILLBOARD
	BounceType "Doom"
	SeeSound "weapons/grenadethrow"
	BounceSound "weapons/icebomb/bounce"
	DeathSound "weapons/icebomb/boom"
	Obituary "$OB_MPGRENADE"
	scale 0.3
	damagetype Ice
	ReactionTime 60	//Just over five seconds
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,24,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Active
	Active:
		ICEG ABCDEF 3 A_CountDown
		loop
	Death:
		TNT1 A 0 A_Explode
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItem("IceExplosion")
		stop
	}
}

// Proximity Mine
// Thanks to amv2k9 on the Zdoom forums for the starting code that I violently butchered
actor ProxyMinePickup : CustomInventory
{
	inventory.pickupmessage "$GOTPROXY"
	Tag "$TAGPROXY"
	inventory.icon "INVPROX"
	inventory.pickupsound "weapons/proxy/get"
	inventory.amount 3
	inventory.maxamount 12
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.INVBAR
	scale 0.5
	states
	{
	Spawn:
		PROX Z 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Use:
		TNT1 A 0 A_FireCustomMissile("ProxyMineProj",0,0)
		stop
	}
}


actor ProxyMineProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 20
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	+ALLOWBOUNCEONACTORS
	+BOUNCEONACTORS
	SeeSound "weapons/proxy/throw"
	BounceSound "weapons/grenadebounce"
	DeathSound "weapons/proxy/land"
	Obituary "$OB_MPGRENADE"
	scale 0.5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,16,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Active
	Active:
		PROX ABCD 3
		loop
	Death:
		PROX A 0
		PROX A 0 A_CheckFloor("SpawnFloor")
		PROX A 0 A_CheckFloor("SpawnCeiling")
	SpawnWall:
		PROX A 0 A_SpawnItem("ProxyMineWall")
		stop
	SpawnFloor:
		PROX A 0 A_SpawnItem("ProxyMineGround")
		stop
	SpawnCeiling:
		PROX A 0 A_SpawnItem("ProxyMineCeiling")
		stop
	}
}

ACTOR ProxyMineGround
{
	MONSTER
	+FRIENDLY
	-COUNTKILL
	-SOLID
	+NOTONAUTOMAP
	+TOUCHY
	+NOBLOOD
	+NOTARGETSWITCH
	+LOOKALLAROUND
	Mass 0x7FFFFFFF
	Height 5
	Radius 8
	BloodType ""
	scale 0.5
	States
	{
	Spawn:
		PROX C 0
		PROX C 0
	SpawnLoop:
		PROX C 1 A_Look
		Loop
	See:
		PROX C 1 A_JumpIfCloser(64,"Death")
		goto SpawnLoop
	Death:
		TNT1 A 0 A_Explode
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItem("Spawner_Explosion_Normal1")
		stop
	}
}

ACTOR ProxyMineWall : ProxyMineGround
{
	+NOGRAVITY
	States
	{
	SpawnLoop:
		PROX A 1 A_Look
		Loop
	See:
		PROX A 1 A_JumpIfCloser(64,"Death")
		goto SpawnLoop
	}
}

ACTOR ProxyMineCeiling : ProxyMineGround
{
	+NOGRAVITY
	States
	{
	SpawnLoop:
		PROX C 1 A_Look
		Loop
	See:
		PROX C 1 A_JumpIfCloser(64,"Death")
		goto SpawnLoop
	}
}

// RTS BASE
actor RTSBasePickup : CustomInventory
{
	inventory.pickupmessage "$GOTRTSBASE"
	Tag "$TAGRTSBASE"
	inventory.icon "INVBASE"
	inventory.pickupsound "misc/ws_pkup"
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.INVBAR
	scale 0.5
	states
	{
	Spawn:
		BASE A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Use:
		TNT1 A 0 A_FireCustomMissile("RTSBaseProj",0,0)
		stop
	}
}

actor RTSBaseProj
{
	Radius 32
	Height 32
	Speed 10
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	//+FORCEXYBILLBOARD
	SeeSound "weapons/grenadethrow"
	//scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_RearrangePointers(AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_DEFAULT)
		TNT1 A 0 ThrustThingZ(0,16,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Active
	Active:
		BASE ABCDE 3
		loop
	Death:
		BASE A 0 A_PlaySound("rts/baseland")
		BASE A 0 A_SpawnItemEx("RTSBase",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		stop
	}
}

actor RTSBase
{
	Radius 32
	Height 32
	Damage 3
	health 100
	//scale 0.5
	damagetype Explosive
	BloodType "MetalSpark"
	mass 0xffff
	+SHOOTABLE
	//+NOBLOOD
	+SOLID
	+DONTGIB
	+NOICEDEATH
	+CANPASS
	+NOBLOODDECALS
	+CANBLAST
	+NOBLOCKMONST
	+FRIENDLY
	States
	{
	Spawn:
		BASE ABCDE 3
		BASE A 0 A_Jump(64, "Attack")
		loop
	Attack:
		BASE A 0
		BASE A 0 A_PlaySound("crate/spawn")
		BASE A 0 A_SpawnItemEx ("RTSTrooper", 0, 0, 35, random(5,15), 0, random(5,15), random(0,360),SXF_TRANSFERPOINTERS)
		BASE ABCDE 3
		goto Spawn
	Death:
		BASE A 0 A_PlaySound("weapons/explode")
		TNT1 A 0 A_Explode
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItem("Spawner_Explosion_Normal1")
		stop
	}
}

ACTOR RTSTrooper : MonsterTemplate
{
	Health 50
	Height 24
	Mass 100
	Speed 3
	PainChance 170
	SeeSound "rtsguy/idle"
	AttackSound "rtsguy/fire"
	PainSound "rtsguy/pain"
	DeathSound "rtsguy/die"
	ActiveSound "rtsguy/idle"
	Obituary "$OB_RTSGUY"
	+FRIENDLY
	scale 0.4
	States
	{
	Spawn:
		PLAY A 0
		PLAY A 0 Thing_ChangeTID(0,900)
		PLAY A 1 A_Look
		PLAY A 0 A_AlertMonsters
		goto See
	See:
		TNT1 A 0 A_JumpIf(score<1,3)
		TNT1 A 0 A_GiveInventory("ScoreItem",score,AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("ScoreItem",-1*score)
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==9,"DogWhistle")
		//PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY A 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY A 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY B 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY B 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY C 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY C 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY D 4 A_Chase
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY D 4 A_Chase
		Loop
	MidAir:
		PLAY DDDDDD 1 A_CheckFloor("See")
		TNT1 A 0 A_SpawnItemEx("CrushDamage")
		PLAY D 1 A_CheckFloor("Landing")
		TNT1 A 0 A_JumpIf(velz==0,"See")
		goto MidAir+6
	Landing:
		PLAY E 1
		PLAY E 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY E 5 A_PlaySound("human/land")
		goto See
	Missile:
		PLAY E 10 A_FaceTarget
		TNT1 A 0 A_JumpIf(score<1,3)
		TNT1 A 0 A_GiveInventory("ScoreItem",score,AAPTR_MASTER)
		TNT1 A 0 A_GiveInventory("ScoreItem",-1*score)
		PLAY FE 4 Bright A_CPosAttack
		PLAY F 1 A_CPosRefire
		Goto Missile+1
	Pain:
		PLAY G 3
		PLAY G 3 A_Pain
		Goto See
	// NORMAL DEATH STATES
	Death:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==4,"XDeath")
		PLAY H 0 A_NoBlocking
		PLAY H 0 A_GiveToTarget("ScoreItem",100)
		PLAY H 5
		PLAY I 4 A_Scream
		PLAY J 4 A_PlaySound("rtsguy/unitlost")
		PLAY K 3
		goto CorpseFalling
	CorpseFalling:
		PLAY JKJKJK 1 A_CheckFloor("Corpse")
		PLAY J 1 A_CheckFloor("CorpseHard")
		TNT1 A 0 A_JumpIf(velz==0,"Corpse")
		goto CorpseFalling+6
	Corpse:
		PLAY L 1 A_PlaySound("human/body")
		PLAY L 1 A_JumpIf(velz!=0,"CorpseFalling")
		TNT1 A 0 A_CheckSight("Cleanup")
		goto Corpse+1
	CorpseHard:
		PLAY L 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto Corpse
	// GIB DEATH STATES
	XDeath:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		PLAY M 0 A_NoBlocking
		PLAY M 0 A_GiveToTarget("ScoreItem",130)
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		PLAY M 0 A_PlaySound("human/gib")
		PLAY M 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		PLAY M 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	// COMEDY DEATH STATES
	Death.Explosive:
		BEGR F 0 A_Jump(64,"XDeath")
		BEGR F 0 A_GiveToTarget("ScoreItem",100)
		TNT1 A 0 A_NoBlocking
		BEGR F 0 ThrustThingZ(0,random(32,128),0,1)
		BEGR F 0 A_Recoil(random(16,48))
		PLAY J 2 A_Scream
		goto CorpseFlying
	CorpseFlying:
		POSS J 0 A_GiveToTarget("ScoreItem",1)
		BEGR G 0 A_SpawnItemEx("BloodShredSilent")
		BEGR G 0 A_SpawnItemEx("MissilePart",0,0,8)
		BEGR G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		PLAY I 1 A_CheckFloor("CorpseCrater")
		BEGR F 0 A_GiveToTarget("ScoreItem",1)
		BEGR H 0 A_SpawnItemEx("BloodShredSilent")
		BEGR H 0 A_SpawnItemEx("MissilePart",0,0,8)
		BEGR G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		PLAY J 1 A_CheckFloor("CorpseCrater")
		loop
	CorpseCrater:
		BEGR M 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		BEGR N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		BEGR N 0 A_PlaySound("human/gib")
		BEGR F 0 A_GiveToTarget("ScoreItem",50)
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BEGR N 0 A_Stop
		goto Corpse+1
	Death.Fire:
		PLAY A 0 A_PlaySound("human/scream")
		PLAY A 0 A_SpawnItemEx("Flame1A",0,0,32)
		PLAY A 0 A_Recoil(-8)
		PLAY A 0 A_SpawnItemEx("Ember1",0,0,32)
		PLAY AAAA 1 A_SetAngle(12 + angle)
		PLAY B 0 A_Recoil(-8)
		PLAY A 0 A_SpawnItemEx("Flame2A",0,0,32)
		PLAY BBBB 1 A_SetAngle(12 + angle)
		PLAY C 0 A_Recoil(-8)
		PLAY A 0 A_SpawnItemEx("Flame1A",0,0,32)
		PLAY CCCC 1 A_SetAngle(12 + angle)
		PLAY D 0 A_Recoil(-8)
		PLAY DDDD 1 A_SetAngle(12 + angle)
		PLAY A 0 A_SpawnItemEx("Flame3B",0,0,32)
		PLAY A 0 A_Jump(128,"Death.Fire")
		goto Death
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		//PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY A 2 A_Wander
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY A 2 A_Wander
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY B 2 A_Wander
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY B 2 A_Wander
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY C 2 A_Wander
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY C 2 A_Wander
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY D 2 A_Wander
		PLAY A 0 A_JumpIf(velz!=0,"MidAir")
		PLAY D 2 A_Wander
		goto See
	}
}