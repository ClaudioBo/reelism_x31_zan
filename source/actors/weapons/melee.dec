// Splot - Melee weapons
ACTOR MeleeWeapon : Weapon
{
	+WEAPON.MELEEWEAPON
	+INVENTORY.ALWAYSPICKUP
}
// Fists
ACTOR Fists : MeleeWeapon
{
	Weapon.Kickback 100
	Weapon.SelectionOrder 3700
	Obituary "$OB_MPFIST"
	Tag "$FIST"
	+WEAPON.WIMPY_WEAPON
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady
		Loop
	Deselect:
		PUNG A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		PUNG A 1 A_Lower
		goto Deselect+1
	Select:
		PUNG A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		PUNG A 1 A_Raise
		goto Select+1
	Fire:
		TNT1 A 0 A_PlaySound("beggar/wiff")
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FireFast")
		PUNG BCDE 1
		PUNG F 1 A_CustomPunch(3,0,0,"FistPuff")
		PUNG GHIIIGEC 1
		PUNG A 5 A_ReFire
		Goto Ready
	FireFast:
		PUNG BD 2
		PUNG F 2 A_CustomPunch(3,0,0,"FistPuff")
		PUNG HIGC 2
		PUNG A 5 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_PlaySound("attack/punchmiss")
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"AltFireFast")
		KICK BCDEF 1
		KICK G 1 A_CustomPunch(10,0,0,"FistPuff")
		KICK HIJJJJJJJJJJJFEDCBA 1
		PUNG A 30
		Goto Ready
	AltFireFast:
		KICK BDF 2
		KICK G 2 A_CustomPunch(10,0,0,"FistPuff")
		KICK IJJJJJFDB 2
		PUNG A 30
		Goto Ready
	}
}

actor FistPuff : PuffBase
{
	damagetype Fists
	States
	{
	Crash:
		PUFF A 0
		PUFF A 0 A_PlaySound("attack/punchwall")
		PUFF CD 4
		stop
	Melee:
		PUFF A 0
		PUFF A 0 A_PlaySound("attack/punch")
		PUFF CD 4
		stop
	}
}

ACTOR Drill : MeleeWeapon
{
	Weapon.Kickback 0
	Weapon.SelectionOrder 2200
	//Weapon.UpSound "weapons/sawup"
	//Weapon.ReadySound "weapons/sawidle"
	Inventory.PickupMessage "$GOTDRILL"
	Tag "$TAGDRILL"
	damagetype Drill
	States
	{
	Ready:
		SDRL A 1 A_WeaponReady
		Loop
	Deselect:
		SDRL A 0 A_PlaySound("weapons/in")
		//TNT1 A 0 A_Lower
		SDRL A 1 A_Lower
		goto Deselect+1
	Select:
		SDRL A 0 A_PlaySound("weapons/out")
		//TNT1 A 0 A_Raise
		SDRL A 1 A_Raise
		goto Select+1
	Fire:
		SDRL B 0 A_PlaySound("weapons/drill",6)
		SDRL B 0 A_Quake(5,2,0,64,"null")
		SDRL BBB 0 A_SpawnItemEx("Particle_Smoke_Normal6",0,0,32,Random(-1,1),Random(-1,1),Random(-1,1))
		SDRL CD 1 A_Saw("null","null",5,"DrillPuff")
		SDRL C 0 A_ReFire
		SDRL B 0 A_PlaySound("weapons/drill",6)
		SDRL BA 1
		SDRL BA 2
		SDRL BA 3
		SDRL BA 4
		SDRL BA 5
		SDRL BA 6
		SDRL BA 7
		Goto Ready
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		SDRP A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

actor DrillPuff : PuffBase
{
	damagetype Drill
}

// Baseball Bat
actor BatPuff : PuffBase
{
	damagetype Bat
	States
	{
	Crash:
		PUFF A 0
		PUFF A 0 A_PlaySound("bat/wall")
		PUFF CD 4
		stop
	Melee:
		PUFF A 0
		PUFF A 0 A_PlaySound("bat/hit")
		PUFF CD 4
		stop
	}
}

ACTOR BaseballBat : MeleeWeapon
{
	Weapon.SelectionOrder 2200
	Weapon.Kickback 100
	Inventory.PickupMessage "$GOTBAT"
	Tag "$TAGBAT"
	States
	{
	Ready:
		BBAT A 1 A_WeaponReady
		Loop
	Deselect:
		BBAT A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		BBAT A 1 A_Lower
		goto Deselect+1
	Select:
		BBAT A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		BBAT A 1 A_Raise
		goto Select+1
	Fire:
		BBAT BC 2
		TNT1 A 8
		TNT1 A 0 A_PlaySound("attack/punchmiss")
		BBAT D 0 A_SetReflective
		BBAT D 1
		BBAT F 1 A_CustomPunch(64,0,0,"BatPuff",72)
		BBAT H 1
		BBAT J 1 A_UnSetReflective
		TNT1 A 4
		BBAT CBAA 2
		Goto Ready
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		BATP A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

// SUPERWEAPON - The Defuckyoulator
ACTOR DefibAmmo : Ammo
{
	Inventory.Amount 10
	Inventory.MaxAmount 10
	Inventory.Icon "HUDSUPER"
	Tag "$TAGSUPER"
}

ACTOR Defib : MeleeWeapon
{
	Weapon.Kickback 100
	Weapon.SelectionOrder 3700
	-INVENTORY.ALWAYSPICKUP
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Inventory.Pickupmessage "$GOTDEFIB"
	Tag "$TAGDEFIB"
	Inventory.PickupSound "misc/ws_pkup"
	Weapon.AmmoType "DefibAmmo"
	AttackSound "defib/zap"
	scale 0.5
	States
	{
	Spawn:
		//TNT1 A 0
		//TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		//TNT1 A 0 A_ChangeFlag("DROPPED",0)
		DFBP A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Ready:
		DEFI A 1 A_WeaponReady
		Loop
	Deselect:
		DEFI A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		DEFI A 1 A_Lower
		goto Deselect+1
	Select:
		DEFI A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		DEFI A 1 A_Raise
		goto Select+1
	Fire:
		TNT1 A 0 A_PlaySound("beggar/wiff")
		DEFI BCDEF 1
		DEFI G 0 A_CustomPunch(400,1,CPF_USEAMMO,"DefibPuff",72)
		DEFI G 30
		DEFI GHIJKLMN 2
		TNT1 A 10
		Goto Ready
	}
}

actor DefibPuff : PuffBase
{
	damagetype Defib
	+PUFFONACTORS
	States
	{
	Crash:
		PUFF A 0
		TNT1 A 0 A_Quake(9,3,0,128,"null")
		PUFF A 0 A_PlaySound("attack/punchwall")
		PUFF CD 4
		stop
	Melee:
		TNT1 A 0
		TNT1 A 0 A_Quake(9,8,0,128,"null")
		TNT1 A 4 A_PlaySound("defib/zap")
		TNT1 A 0 A_SpawnItemEx ("DefibSkel",48,0,0,48,0,0,180,SXF_TRANSFERPOINTERS)
		PUFF CD 4
		stop
	}
}

actor DefibSkel
{
	Radius 12
	Height 12
	Speed 20
	Damage 20
	Projectile
	+RANDOMIZE
	//+FORCEXYBILLBOARD
	SeeSound "weapons/rpg"
	DeathSound "weapons/explode"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	damagetype Explosive
	States
	{
	Spawn:
		SKLT A 0
		TNT1 A 0 //A_PlaySound("defib/zap")
		TNT1 A 0 //A_Quake(9,8,0,128,"null")
	Fly:
		SKLT A 0 A_SetPitch(0)
		SKLT A 0 A_CustomMissile("BatGib3",random(4,16),0,random(0,360),2,random(4,32))
		SKLT A 1 bright A_SpawnItemEx("MissilePart",0,0,8)
		SKLT A 0 A_CustomMissile("BatGib1",random(4,16),0,random(0,360),2,random(4,32))
		SKLT A 1 bright A_SpawnItemEx("MissilePart",0,0,8)
		loop
	Death:
		TNT1 A 0 A_Explode
		TNT1 AAAAAA 0 A_CustomMissile("BatGib1",random(4,16),0,random(0,360),2,random(4,32))
		TNT1 AAAAAA 0 A_CustomMissile("BatGib3",random(4,16),0,random(0,360),2,random(4,32))
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItemEx("Spawner_Explosion_Normal2")
		stop
	}
}

// Bottle!
ACTOR MasterSword : MeleeWeapon
{
	Weapon.Kickback 100
	Weapon.SelectionOrder 3700
	Inventory.PickupMessage "$GOTSWORD"
	Obituary "$OB_MPFIST"
	Tag "$TAGSWORD"
	scale 2
	Weapon.UpSound "weapons/out"
	States
	{
	Spawn:
		MSWO Z 1050 BRIGHT
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		MSWO A 1 A_WeaponReady
		Loop
	Deselect:
		MSWO A 0 A_PlaySound("weapons/in")
		MSWO A 0 A_PlaySound("weapons/mastersword/lower")
		TNT1 A 0 A_Lower
		MSWO A 1 A_Lower
		goto Deselect+2
	Select:
		MSWO A 0 A_PlaySound("weapons/mastersword/draw")
		TNT1 A 0 A_Raise
		MSWO A 1 A_Raise
		goto Select+1
	Fire:
		MSWO A 0 A_JumpIf (health >= 100, "FireMagic")
		MSWO A 1 Offset (0,32) A_PlaySound("weapons/mastersword/swing")
		MSWO B 0 A_ChangeFlag("REFLECTIVE", TRUE)
		MSWO B 0 A_ChangeFlag("AIMREFLECT", TRUE)
		MSWO B 0 A_ChangeFlag("INVULNERABLE", TRUE)
		MSWO B 1 Offset (0,32)
		MSWO B 1 Offset (-8,36)
		MSWO C 1 Offset (0,32) A_CustomPunch(64,0,0,"MasterSwordPuff",72)
		MSWO C 1 Offset (-12,46)
		MSWO C 1 Offset (-36,62)
		MSWO B 0 A_ChangeFlag("REFLECTIVE", FALSE)
		MSWO B 0 A_ChangeFlag("AIMREFLECT", FALSE)
		MSWO B 0 A_ChangeFlag("INVULNERABLE", FALSE)
		MSWO C 1 Offset (-48,70)
		MSWO C 1 Offset (-48,71)
		MSWO C 1 Offset (-49,72)
		MSWO C 1 Offset (-49,73)
		MSWO C 1 Offset (-50,74)
		MSWO C 1 Offset (-50,75)
		MSWO C 1 Offset (-51,76)
		MSWO C 1 Offset (-51,77)
		MSWO C 1 Offset (-52,78)
		MSWO B 1 Offset (-8,36)
		MSWO B 1 Offset (0,32)
		MSWO A 4 Offset (0,32)
		Goto Ready
	FireMagic:
		MSWO A 1 Offset (0,32) A_PlaySound("weapons/mastersword/swing")
		MSWO B 0 A_ChangeFlag("REFLECTIVE", TRUE)
		MSWO B 0 A_ChangeFlag("AIMREFLECT", TRUE)
		MSWO B 0 A_ChangeFlag("INVULNERABLE", TRUE)
		MSWO B 1 Offset (0,32)
		MSWO B 1 Offset (-8,36)
		MSWO C 0 A_FireCustomMissile("MasterSwordBall")
		MSWO C 1 Offset (0,32) A_CustomPunch(64,0,0,"MasterSwordPuff",72)
		MSWO C 1 Offset (-12,46)
		MSWO C 1 Offset (-36,62)
		MSWO B 0 A_ChangeFlag("REFLECTIVE", FALSE)
		MSWO B 0 A_ChangeFlag("AIMREFLECT", FALSE)
		MSWO B 0 A_ChangeFlag("INVULNERABLE", FALSE)
		MSWO C 1 Offset (-48,70)
		MSWO C 1 Offset (-48,71)
		MSWO C 1 Offset (-49,72)
		MSWO C 1 Offset (-49,73)
		MSWO C 1 Offset (-50,74)
		MSWO C 1 Offset (-50,75)
		MSWO C 1 Offset (-51,76)
		MSWO C 1 Offset (-51,77)
		MSWO C 1 Offset (-52,78)
		MSWO B 1 Offset (-8,36)
		MSWO B 1 Offset (0,32)
		MSWO A 4 Offset (0,32)
		Goto Ready
	}
}

actor MasterSwordPuff : PuffBase
{
	damagetype Fists
	States
	{
	Crash:
		PUFF A 0
		PUFF A 0 A_PlaySound("weapons/mastersword/clang")
		PUFF CD 4
		stop
	Melee:
		PUFF A 0
		PUFF A 0 A_PlaySound("weapons/mastersword/hit")
		PUFF CD 4
		stop
	}
}

ACTOR MasterSwordBall
{
	Radius 13
	Height 8
	Speed 25
	Damage 5
	Projectile
	XScale 1.5
	YScale 0.5
	+RANDOMIZE
	+BLOODSPLATTER
	RenderStyle Add
	Alpha 0.75
	SeeSound "weapons/mastersword/magic"
	DeathSound "weapons/mastersword/magichit"
	States
	{
	Spawn:
		HTPA A 0
		HTPA A 0 A_PlaySound("weapons/mastersword/magicloop")
		HTPA A 6 Bright A_SpawnItemEx("MasterSwordSpark1")
		HTPA B 0 A_PlaySound("weapons/mastersword/magicloop")
		HTPA B 6 Bright A_SpawnItemEx("MasterSwordSpark1")
		Loop
	Death:
		PLSE ABCDE 4 Bright
		Stop
	}
}

ACTOR MasterSwordSpark1
{
	Mass 1
	Radius 1
	Height 1
	Scale 1
	Gravity .125
	+NOBLOCKMAP
	+FLOORCLIP
	+NOTELEPORT
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPRK A 0
		SPRK A 2 
		SPRK B 2
		SPRK C 2 A_SpawnDebris("MasterSwordSpark2",1)
		SPRK D 2
		Stop
	}
}

ACTOR MasterSwordSpark2 : MasterSwordSpark1
{
	Gravity 1
	Health 4
	radius 3
	height 6
	speed .1
	Scale 1
	Mass 0
	BounceType Doom
	+DONTSPLASH
	States
	{
	Spawn:
		SPRK ABCDABCDABCDABCD 1
		SPRK AB 1 A_SetTranslucent(.8)
		SPRK CD 1 A_SetTranslucent(.6)
		SPRK AB 1 A_SetTranslucent(.4)
		SPRK CD 1 A_SetTranslucent(.2)
		Stop
	}
}