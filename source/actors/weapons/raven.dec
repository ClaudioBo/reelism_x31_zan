// Splot - Raven Shit fuck me it's way too hot in here

// crossbow
ACTOR RavenCrossbow : Weapon
{
	Weapon.SelectionOrder 800
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "RavenCrossbowAmmo"
	Weapon.YAdjust 15
	Inventory.PickupMessage "$GOTCROSSBOW"
	Tag "$TAGCROSSBOW"
	Inventory.PickupSound "weapons/ravenget"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		WBOW A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		CRBW AAAAAABBBBBBCCCCCC 1 A_WeaponReady
		Loop
	Deselect:
		CRBW A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		CRBW A 1 A_Lower
		goto Deselect+1
	Select:
		CRBW A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		CRBW A 1 A_Raise
		goto Select+1
	Fire:
		CRBW D 0 A_FireCustomMissile("RavenCrossbowFX1")
		CRBW D 0 A_FireCustomMissile("RavenCrossbowFX2",4.5,0)
		CRBW D 0 A_FireCustomMissile("RavenCrossbowFX2",-4.5,0)
		CRBW D 6 //A_FireCrossbowPL1
		CRBW EFGH 3
		CRBW AB 4
		CRBW C 5 A_ReFire
		Goto Ready
	}
}

ACTOR RavenCrossbowFX1
{
	Radius 11
	Height 8
	Speed 30
	Damage 10
	Projectile
	RenderStyle Add
	SeeSound "weapons/bowshoot"
	DeathSound "weapons/bowhit"
	Obituary "$OB_MPCROSSBOW"
	States
	{
	Spawn:
		FX03 B 1 BRIGHT
		Loop
	Death:
		FX03 HIJ 8 BRIGHT
		Stop
	}
}

ACTOR RavenCrossbowFX2 : RavenCrossbowFX1
{
	Speed 32
	Damage 6
	Obituary "$OB_MPPCROSSBOW"
	States
	{
	Spawn:
		FX03 B 1 BRIGHT A_SpawnItemEx("RavenCrossbowFX4",random2[BoltSpark]()*0.015625,random2[BoltSpark]()*0.015625,0,0,0,0,0,SXF_ABSOLUTEPOSITION,50)
		Loop
	}
}

ACTOR RavenCrossbowFX4
{
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Gravity 0.125
	RenderStyle Add
	States
	{
	Spawn:
		FX03 FG 8 BRIGHT
		Stop
	}
}

// mace
ACTOR RavenMace : Mace
{
	Weapon.SelectionOrder 1400
	Weapon.AmmoUse 1
	Weapon.AmmoGive1 50
	Weapon.YAdjust 15
	Weapon.AmmoType "RavenMaceAmmo"
	Inventory.PickupMessage "$GOTFIREMACE"
	Inventory.PickupSound "weapons/ravenget"
	Tag "$TAGFIREMACE"
	Weapon.SisterWeapon ""
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		WMCE A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		MACE A 1 A_WeaponReady
		Loop
	Deselect:
		MACE A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		MACE A 1 A_Lower
		goto Deselect+1
	Select:
		MACE A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		MACE A 1 A_Raise
		goto Select+1
	}
}
ACTOR RavenMaceFX1 : MaceFX1 replaces MaceFX1
{
	+CANBOUNCEWATER
}
ACTOR RavenMaceFX2 : MaceFX2 replaces MaceFX2
{
	+CANBOUNCEWATER
}
ACTOR RavenMaceFX3 : MaceFX3 replaces MaceFX3
{
	+CANBOUNCEWATER
}

// Hexen Hammer
ACTOR RavenHammer : FWeapHammer
{
	+BLOODSPLATTER
	Weapon.SelectionOrder 900
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.MELEEWEAPON
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.MinSelectionAmmo1 1
	Weapon.KickBack 150
	Weapon.YAdjust -10
	Weapon.AmmoType "RavenManaAmmo"
	Inventory.PickupMessage "$GOTHAMMER"
	Inventory.PickupSound "weapons/ravenget"
	Tag "$TAGHAMMER"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		WFHM A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Deselect:
		FHMR A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		FHMR A 1 A_Lower
		goto Deselect+1
	Select:
		FHMR A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		FHMR A 1 A_Raise
		goto Select+1
	Ready:
		FHMR A 1 A_WeaponReady
		Loop
	Fire:
		FHMR BC 2
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FireFast")
		FHMR D 1 A_FHammerAttack
		FHMR EF 1
		FHMR G 1 A_FHammerThrow
		FMHR H 1
		TNT1 A 10
		FHMR A 1 Offset(0,60)
		FHMR A 1 Offset(0,55)
		FHMR A 1 Offset(0,50)
		FHMR A 1 Offset(0,45)
		FHMR A 1 Offset(0,40)
		FHMR A 2 Offset(0,35)
		Goto Ready
	FireFast:
		FHMR D 2 A_FHammerAttack
		FHMR F 1
		FHMR G 2 A_FHammerThrow
		TNT1 A 10
		FHMR A 2 Offset(0,60)
		FHMR A 2 Offset(0,50)
		FHMR A 2 Offset(0,40)
		FHMR A 2 Offset(0,35)
		Goto Ready
	}
}

ACTOR RavenHammerMissle replaces HammerMissile
{
	Speed 25
	Radius 14
	Height 20
	Damage 10
	Projectile
	DeathSound "FighterHammerExplode"
	Obituary "$OB_MPFWEAPHAMMERR"
	damagetype Explosive
	States
	{
	Spawn:
		FHFX A 2 Bright
		FHFX B 2 Bright A_PlaySound("FighterHammerContinuous")
		FHFX CDEFGH 2 Bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 0 A_SpawnItemEx("Spawner_Explosion_Normal1")
		TNT1 A 0 A_Explode(128,128,0)
		FHFX I 3 Bright A_SetTranslucent(1,1)
		FHFX J 3 Bright
		FHFX K 3 Bright
		FHFX LM 3 Bright
		FHFX N 3
		FHFX OPQR 3 Bright
		Stop
	}
}

ACTOR RavenHammerPuff replaces HammerPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	RenderStyle "Translucent"
	Alpha 0.6
	VSpeed 0.8
	SeeSound "FighterHammerHitThing"
	AttackSound "FighterHammerHitWall"
	ActiveSound "FighterHammerMiss"
	States
	{
	Spawn:
		FHFX STUVW 4
		Stop
	}
}

// Superweapon - Wraithverge
ACTOR RavenWraithvergeAmmo : Ammo
{
	Inventory.Amount 10
	Inventory.MaxAmount 10
	Inventory.Icon "HUDSUPER"
	Tag "$TAGSUPER"
}

ACTOR RavenWraithverge : CWeapWraithverge
{
	Health 3
	Weapon.SelectionOrder 3000
	+WEAPON.PRIMARY_USES_BOTH
	+Inventory.NoAttenPickupSound
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 10
	Weapon.KickBack 150
	Weapon.AmmoType1 "RavenWraithvergeAmmo"
	Weapon.AmmoType2 "RavenWraithvergeAmmo"
	Inventory.PickupMessage "$GOTWRAITHVERGE"
	Tag "$TAGWRAITHVERGE"
	Inventory.PickupSound "misc/ws_pkup"
	States
	{
	Spawn:
		//TNT1 A 0
		//TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		//TNT1 A 0 A_ChangeFlag("DROPPED",0)
		WCHS A 1050 BRIGHT
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		CHLY A 1 A_WeaponReady
		Loop
	Deselect:
		CHLY A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		CHLY A 1 A_Lower
		goto Deselect+1
	Select:
		CHLY A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		CHLY A 1 A_Raise
		goto Select+1
	Fire:
		CHLY AB 1 Bright Offset (0, 40)
		CHLY CD 2 Bright Offset (0, 43)
		CHLY E 2 Bright Offset (0, 45)
		CHLY F 6 Bright Offset (0, 48) A_CHolyAttack
		CHLY GG 2 Bright Offset (0, 40) A_CHolyPalette
		CHLY G 2 Offset (0, 36) A_CHolyPalette
		Goto Ready
	}
}