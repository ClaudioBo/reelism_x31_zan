// Splot - HACX
// All weapons and code courtesy of the HACX 2.0 dev team!

// Taser
actor HacxTazer : Weapon
{
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	inventory.pickupmessage "$GOTTAZER"
	weapon.ammotype "DoomShell"
	Tag "$TAGTAZER"
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		TAZR A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		TAZG A 1 A_WeaponReady
		loop
	Deselect:
		TAZG A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		TAZG A 1 A_Lower
		goto Deselect+1
	Select:
		TAZG A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Raise
		TAZG A 1 A_Raise
		goto Select+1
	Fire:
		TAZG B 0 A_PlaySound("weapons/tazercharge")
		TAZG A 3 A_GunFlash
		TAZG A 0 A_PlaySound("weapons/tazer",CHAN_WEAPON)
		TAZF A 3 bright A_FireCustomMissile("TazerShot")
		TAZF B 2 bright
		TAZG C 2
		TAZG D 3
		TAZG E 5
		TAZG D 4
		TAZG C 3
		TAZG A 4
		TAZG A 1 A_ReFire
		goto Ready
	Flash:
		TAZC A 2 bright
		TAZC B 2 bright A_Light1
		TAZC C 2 bright A_Light2
		TNT1 A 2
		Goto Lightdone
	}
}

actor TazerShot : PlasmaBall
{
	//game Hacx
	Speed 50
	damage 10
	seesound "weapons/tazerzap"
	deathsound "weapons/tazerhit"
	decal "TazerScorch"
	states
	{
	Spawn:
		TBAL AAABBBCCC 1 bright A_SpawnItemEx("TazerTrail1",0,.1*random(-10,10),.1*random(-10,10)-2)
		loop
	Death:
		TPUF AB 3 bright
		TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0,0   + random(-5,5),SXF_TRANSFERPOINTERS,0)
		TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0,72  + random(-5,5),SXF_TRANSFERPOINTERS,0)
		TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0,144 + random(-5,5),SXF_TRANSFERPOINTERS,0)
		TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0,216 + random(-5,5),SXF_TRANSFERPOINTERS,0)
		TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0,288 + random(-5,5),SXF_TRANSFERPOINTERS,0)
		TPUF CDE 3 bright
		stop
	}
}

actor TazerFrag : TazerShot
{
	//game Hacx
	speed 20
	damage 6
	deathsound ""
	decal "TazerScorch"
	states
	{
	Spawn:
		TPUF AABBBAA 1 bright A_SpawnItemEx("TazerTrail2",0,.5*random(-10,10),.5*random(-10,10))
		stop
	XDeath:
		TPUF C 0 A_PlaySound("weapons/tazerfire")
	Death:
		TPUF CDE 2 bright
		//TZAP DEFGHI 2 bright
		stop
	}
}

Actor TazerTrail1 : BulletPuff
{
	Alpha .5
	Scale .75
	Renderstyle Add
	VSpeed 0
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TZAP EEEDDDCCCBBBAAA 1 bright A_FadeOut(.05)
		Stop
	}
}

Actor TazerTrail2 : TazerTrail1
{
	Alpha 1
	Scale 1
	States
	{
	Spawn:
		TZAP AABBAA 1 bright A_FadeOut(.10)
		Stop
	}
}

Actor TazerPuff : BulletPuff
{
	+PUFFONACTORS
	Renderstyle Add
	VSpeed 0
	States
	{
	Spawn:
	Melee:
		TPUF ABCDE 3 bright
		Stop
	}
}

// Uzis

actor HacxUzi : CustomInventory
{
	inventory.pickupmessage "$GOTUZI"
	inventory.pickupsound "misc/w_pkup"
	states
	{
	Spawn:
		UZII A 1050 BRIGHT
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("HacxUzi1",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("HacxUzi1")
		stop
	Pickup2:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(676,0,0,0)== 1,"Grab")
		TNT1 A 0 A_GiveInventory("HacxUzi2")
		stop
	Grab:
		TNT1 A 0 A_JumpIfInventory("HacxUzi2", 1, 3)
		TNT1 A 0 A_GiveInventory("HacxUzi2")
		TNT1 A 0 A_SpawnItemEx("DummyThingHacxUzi")
		TNT1 A 0
		Fail
	}
}

Actor DummyThingHacxUzi : FakeInventory
{
	inventory.pickupmessage "$GOTUZI"
	Inventory.PickupSound "misc/w_pkup"
	States
	{
	Spawn:
		TNT1 A 2
		Stop
	}
}

actor HacxUzi1 : Weapon
{
	Weapon.SelectionOrder 700
	attacksound "weapons/uzi"
	inventory.pickupmessage "$GOTUZI"
	weapon.ammotype "DoomClip"
	weapon.ammotype2 "DoomClip"
	weapon.ammouse 1
	weapon.ammouse2 1
	Weapon.AmmoGive 50
	Tag "$TAGUZI"
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		UZII A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		UZIG A 1 A_WeaponReady
		loop
	Deselect:
		UZIG A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		UZIG A 1 A_Lower
		goto Deselect+1
	Select:
		UZIG A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Raise
		UZIG A 1 A_Raise
		goto Select+1
	Fire:
	Hold:
		UZIF A 0 A_GunFlash
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,8,-9)
		UZIF A 2 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,8,-9)
		UZIF B 2 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZIG B 0 A_CheckForReload(25,"Refire")
		// intentional fall-through
	Reload:
		UZIG B 3 A_JumpIfNoAmmo("Ready")
		UZIG C 3
		UZIG D 3 A_ResetReloadCounter
		UZIG E 8 A_PlaySound("weapons/uzireload",CHAN_WEAPON)
		UZIG CBA 3
		// ditto
	Refire:
		UZIG B 0 A_ReFire
		goto Ready
	AltFire:
		UZIG A 1 A_WeaponReady(WRF_NOFIRE)
		UZIG A 0 A_CheckForReload(1,"Ready",1)
		goto Reload
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		Goto LightDone
	}
}

actor HacxUzi2 : HacxUzi1
{
	Weapon.SelectionOrder 699
	attacksound ""
	inventory.pickupmessage "$GOTUZI"
	Tag "$TAGUZI2"
	Weapon.AmmoGive 100
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		UZII A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		UZI2 A 1 A_WeaponReady
		loop
	Deselect:
		UZI2 A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		UZI2 A 1 A_Lower
		goto Deselect+1
	Select:
		UZI2 A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Raise
		UZI2 A 1 A_Raise
		goto Select+1
	Fire:
	Hold:
		UZI2 B 0 A_GunFlash
		UZI2 A 0 A_PlaySound("weapons/uzi")
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,12,9)
		UZI2 B 0 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",-5,0,-12,9)
		UZI2 B 1 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZI2 B 1 A_PlaySound("weapons/uzi",CHAN_WEAPON)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,12,9)
		UZI2 C 0 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",-5,0,-12,9)
		UZI2 C 2 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZI2 C 0 A_CheckForReload(25,1)
		goto Reload
		UZI2 C 0 A_JumpIfNoAmmo("Ready")
		UZI2 C 0 A_PlaySound("weapons/uzi")
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,12,9)
		UZI2 D 0 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",-5,0,-12,9)
		UZI2 D 1 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZI2 D 1 A_PlaySound("weapons/uzi",CHAN_WEAPON)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,12,9)
		UZI2 E 0 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZIF A 0 A_FireCustomMissile("BulletCasingSpawn",-5,0,-12,9)
		UZI2 E 2 bright A_FireBullets(5.6,0,-1,5,"HandgunPuff",FBF_USEAMMO|FBF_NORANDOM)
		UZI2 C 0 A_CheckForReload(25,"Refire")
		// intentional fall-through
	Reload:
		UZI2 F 3 A_JumpIfNoAmmo("Ready")
		UZI2 GH 3
		UZIG D 3 A_ResetReloadCounter
		UZIG E 8 A_PlaySound("weapons/uzireload",CHAN_WEAPON)
		UZIG CB 3
		UZI2 IJKL 3
		UZI2 M 8 A_PlaySound("weapons/uzireload",CHAN_WEAPON)
		UZI2 NOP 3
		// ditto
	Refire:
		UZI2 A 0 A_ReFire
		goto Ready
	AltFire:
		UZI2 A 1 A_WeaponReady(WRF_NOFIRE)
		UZI2 A 0 A_CheckForReload(1,"Ready",1)
		goto Reload
	}
}

// The Anti-Gun
actor HacxAntigun : Weapon
{
	inventory.pickupmessage "$GOTANTIGUN"
	weapon.ammotype "DoomCell"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Tag "$TAGANTIGUN"
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		ANTI A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	Ready:
		ANTG A 1 A_WeaponReady
		Loop
	Deselect:
		//ANTG A 0 A_Lower
		ANTG A 1 A_Lower
		Loop
	Select:
		//ANTG A 0 A_Raise
		ANTG A 1 A_Raise
		Loop
	Fire:
		ANTG A 0 A_GunFlash
		ANTF AB 2 A_FireCustomMissile("HacxMolecule")
		ANTG B 20 A_ReFire
		goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 B 3 A_Light2
		Goto LightDone
	}
}

actor HacxMolecule : FastProjectile
{
	Radius 13
	Height 8
	Speed 100
	Damage (8)
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 1.0
	seesound "weapons/antigun"
	deathsound "weapons/antigunhit"
	MissileType "HacxMoleculeTrail"
	MissileHeight 4
	//FOLLOWING FLAGS ARE EXPERIMENTAL NERFS
	+NODAMAGETHRUST
	+PAINLESS
	states
	{
	Spawn:
		MOLS AB 2 bright
		loop
	Death:
		MOLE ABCD 3 bright
		Stop
	}
}

Actor HacxMoleculeTrail : BulletPuff
{
	Alpha 1
	Renderstyle Add
	VSpeed 0
	States
	{
	Spawn:
		MOLS AB 1 bright A_FadeOut(.33,1)
		loop
	}
}

// Superweapon - Nuker
ACTOR HacxNukerAmmo : Ammo
{
	Inventory.Amount 5
	Inventory.MaxAmount 5
	Inventory.Icon "HUDSUPER"
	Tag "$TAGSUPER"
}

actor HacxNuker : BFG9000
{
	height 20
	inventory.pickupmessage "$GOTNUKER"
	+WEAPON.BFG
	weapon.ammotype "HacxNukerAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Tag "$TAGNUKER"
	Inventory.PickupSound "misc/ws_pkup"
	+NOAUTOAIM
	states
	{
	Spawn:
		//TNT1 A 0
		//TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		//TNT1 A 0 A_ChangeFlag("DROPPED",0)
		NUKR A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Deselect:
		NUKG A 0 A_StopSound(6)
		NUKG A 0 A_PlaySound("weapons/in")
		//NUKG A 0 A_Lower
		NUKG A 1 A_Lower
		goto Deselect+2
	Select:
		NUKG A 0 A_StopSound(6)
		NUKG A 0 A_PlaySound("weapons/out")
		//NUKG A 0 A_Raise
		NUKG A 1 A_Raise
		goto Select+2
	Ready:
		NUKG A 1 A_WeaponReady
		Loop
	Fire:
		NUKG A 0 A_PlaySound("weapons/nuker",CHAN_WEAPON)
		NUKG CADACADCDCDCDCDA 1
		NUKG A 1 Offset(0,34)
		NUKG A 1 Offset(0,37)
		NUKG A 1 Offset(0,43)
		NUKG B 0 A_GunFlash
		NUKG B 1 Offset(0,45) A_FireCustomMissile("HacxNukeShot",0,1,0,-8,0,10)
		NUKG B 1 Offset(0,47)
		NUKG B 1 Offset(0,48)
		NUKG B 1 Offset(0,49)
		NUKG B 1 Offset(0,48)
		NUKG B 1 Offset(0,47)
		NUKG B 1 Offset(0,45)
		NUKG A 1 Offset(0,43)
		NUKG A 1 Offset(0,39)
		NUKG A 1 Offset(0,37)
		NUKG A 1 Offset(0,35)
		NUKG A 1 Offset(0,34)
		NUKG A 1 Offset(0,33)
		NUKG A 9 Offset(0,32)
		NUKG A 0 A_Refire
		goto Ready
	Flash:
		NUKF A 3 bright A_Light1
		NUKF B 4 bright A_Light2
		Goto LightDone
	}
}

Actor HacxNukeShot : BFGBall
{
	+CANBOUNCEWATER
	//game Hacx
	Damage 10
	Speed 30
	Scale .5
	Renderstyle Normal
	Bouncetype Hexen
	Bouncefactor .5
	Wallbouncefactor .25
	-NOGRAVITY
	+BOUNCEONACTORS
	Decal BFGScorch
	DeathSound ""
	BounceSound ""
	damagetype Explosive
	States
	{
	Spawn:
		NCAN ABCDABCDABCDABCD 2 bright
		NCAN A 0 A_Stop
		NCAN A 0 A_Die
		// Fallthrough, just in case.
	Death:
		NUK1 A 0 A_NoGravity
		NUK1 A 0 A_Explode(128,256)
		NUK1 A 0 A_PlaySound("weapons/nukehit",0,1.0,0,.5)
		TNT1 A 5 A_SpawnItemEx("HacxNukeBoom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Mushroom("HacxNukeSpray",50,MSF_Classic,.5,1.0)
		TNT1 A 5 A_Mushroom("HacxNukeSmoke",20,MSF_Classic,2.0,.2)
		Stop
	}
}

actor HacxNukeBoom
{
	//game Hacx
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 1
	States
	{
	Spawn:
		NUK1 ABCDEF 5 Bright
		Stop
	}
}

actor HacxNukeSmoke : PlasmaBall
{
	//game Hacx
	+NOBLOCKMAP
	+NOGRAVITY
	+THRUACTORS
	Damage 0
	Speed 15
	RenderStyle Add
	Alpha .5
	Gravity .5
	SeeSound ""
	States
	{
	Spawn:
		NUK1 A 0
		NUK1 A 0 A_ScaleVelocity(1.0+.05*random(-5,5))
		NUK1 A 0 A_Jump(128,2)
		NUK2 D 0
		"----" A 16 bright
	Death:
		"----" A 0 A_ScaleVelocity(1.5)
		"----" A 4 bright A_FadeOut(.125)
		Loop
	}
}

actor HacxNukeSpray : PlasmaBall
{
	//game Hacx
	Speed 15
	Damage 5
	SeeSound ""
	+BOUNCEONWALLS
	Wallbouncefactor .5
	Damagetype Fire
	states
	{
	Spawn:
		NBAL A 0
		NBAL A 0 A_ScaleVelocity(1.0+.05*random(-5,5))
		NBAL AB 2 bright
		loop
	Death:
		NUK2 B 4 bright
		NUK2 A 4 bright A_Explode(20,64,1,1,64)
		NUK2 BCD 4 bright
		stop
	}
}