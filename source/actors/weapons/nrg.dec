// Splot - Energy Weapons

ACTOR DoomPlasmaRifle : Weapon
{
	Weapon.SelectionOrder 100
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "DoomCell"
	Inventory.PickupMessage "$GOTPLASMA"
	Tag "$TAGPLASMA"
	States
	{
	Ready:
		PLSG A 1 A_WeaponReady
		Loop
	Deselect:
		PLSG A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		PLSG A 1 A_Lower
		goto Deselect+1
	Select:
		PLSG A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		PLSG A 1 A_Raise
		goto Select+1
	Fire:
		PLSF A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FastFire")
		PLSF A 1 A_FireCustomMissile("DoomPlasmaBall")
		PLSF BCD 1
		PKPL B 1 A_ReFire
		PKPL DEF 1
		PKPL GF 3
		PKPL EDCBA 2
		Goto Ready
	FastFire:
		PLSF A 1 A_FireCustomMissile("DoomPlasmaBall")
		PLSF D 1
		PKPL B 1 A_ReFire
		PKPL DFGFEDCBA 1
		Goto Ready
	Flash:
		PLSF B 2 Bright A_Light1
		PLSF A 1 Bright A_Light1
		TNT1 A 1
		Goto LightDone
		PLSF C 2 Bright A_Light1
		PLSF D 1 Bright A_Light1
		TNT1 A 1
		Goto LightDone
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		PLAS A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

ACTOR DoomPlasmaBall
{
	Radius 13
	Height 8
	Speed 25
	Damage 5
	Projectile
	+RANDOMIZE
	+BLOODSPLATTER
	RenderStyle Add
	Alpha 0.75
	SeeSound "weapons/plasmaf"
	DeathSound "weapons/plasmax"
	Obituary "$OB_MPPLASMARIFLE"
	States
	{
	Spawn:
		PLSS AB 6 Bright
		Loop
	Death:
		PLSE ABCDE 4 Bright
		Stop
	}
}

ACTOR LaserShot
{
	Radius 8
	Height 8
	Speed 12
	Damage 4
	Projectile
	+RANDOMIZE
	+BLOODSPLATTER
	//+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.75
	scale 0.3
	SeeSound "laser/fire"
	DeathSound "laser/hit"
	Obituary "$OB_MPPLASMARIFLE"
	damagetype Enemy
	States
	{
	Spawn:
		EMBR A 1 Bright A_SpawnItemEx("LaserTrail")
		Loop
	Death:
		TNT1 A 0
		TNT1 A 1
		Stop
	}
}

ACTOR LaserTrail
{
	+NOGRAVITY
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	RenderStyle Add
	Alpha 0.75
	scale 0.3
	States
	{
	Spawn:
		EMBR A 3 Bright
		EMBR A 1 A_FadeOut(0.2)
		goto Spawn+1
	}
}

ACTOR NewRailgun : Weapon
{
	Weapon.SelectionOrder 100
	Weapon.AmmoUse 1
	Weapon.AmmoUse 10
	Weapon.AmmoGive 40
	Weapon.AmmoType "DoomCell"
	Inventory.PickupMessage "$GOTRAIL"
	Tag "$TAGRAIL"
	States
	{
	Ready:
		RAIL A 1 A_WeaponReady
		Loop
	Deselect:
		RAIL A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		RAIL A 1 A_Lower
		goto Deselect+1
	Select:
		RAIL A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		RAIL A 1 A_Raise
		goto Select+1
	Fire:
		RAIL B 3 A_RailAttack(100,0,1,"00 00 ff","ff ff ff",RGF_FULLBRIGHT,0,"BulletPuff")
		RAIL CDEEEEEFGHI 2
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 4 A_Light1
		Goto LightDone
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		RLGN A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

ACTOR BeamGun : Weapon
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "DoomCell"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTBEAM"
	Tag "$TAGBEAM"
	States
	{
	Ready:
		LASR A 1 A_WeaponReady
		Loop
	Deselect:
		LASR A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		LASR A 1 A_Lower
		goto Deselect+1
	Select:
		LASR A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		LASR A 1 A_Raise
		goto Select+1
	Fire:
		LASR A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==3,"FastFire")
		LASR B 0 A_PlaySound("laser/fire")
		LASR B 1 bright A_RailAttack(20,0,0,green,none,RGF_SILENT|RGF_NOPIERCING,0,"HandgunPuff")
		LASR B 0 A_GunFlash
		LASR BC 1 bright
		LASR DEFAAAA 2
		BAZO A 0 A_ReFire
		Goto Ready
	FastFire:
		LASR B 0 A_PlaySound("laser/fire")
		LASR B 1 bright A_RailAttack(20,0,0,green,none,RGF_SILENT|RGF_NOPIERCING,0,"HandgunPuff")
		LASR B 0 A_GunFlash
		LASR C 1 bright
		LASR DFAAA 2
		BAZO A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 7 A_Light1
		TNT1 A 8 A_Light2
		Goto LightDone
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		PULG A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

actor Thunderbolt : Weapon
{
	obituary "%o got roasted, and not by the Friars Club."
	inventory.pickupmessage "$GOTTHUNDERBOLT"
	Tag "$TAGTHUNDERBOLT"
	weapon.selectionorder 700
	weapon.kickback 100
	weapon.ammotype "DoomCell"
	weapon.ammouse 1
	weapon.ammogive 100
	states
	{
	Ready:
		THUN A 0 A_StopSound(6)
		THUN A 1 A_WeaponReady
		goto Ready+1
	Deselect:
		THUN A 0 A_StopSound(6)
		THUN A 0 A_PlaySound("weapons/in")
		TNT1 A 0 A_Lower
		THUN A 1 A_Lower
		goto Deselect+2
	Select:
		THUN A 0 A_StopSound(6)
		THUN A 0 A_PlaySound("weapons/out")
		TNT1 A 0 A_Raise
		THUN A 1 A_Raise
		goto Select+2
	Fire:
		THUN AB 2
		THUN C 0 A_PlaySound("weapons/lightning/start",7)
	Hold:
		THUN C 0 A_JumpIf(waterlevel >= 2,"InWater")
		THUN C 0 A_RailAttack(3,0,1,"ff ff ff","ff ff ff",RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_SILENT,16,"Thunderpuff",0,0,512,1,0.3,0)
		THUN C 0 A_PlaySound("weapons/lightning/loop",6,1,1)
		THUN CDC 1 A_RailAttack(3,0,0,"ff ff ff","ff ff ff",RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_SILENT,16,"Thunderpuff",0,0,512,1,0.3,0)
		THUN DCD 1 A_RailAttack(3,0,0,"cc cc ff","cc cc ff",RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_SILENT,16,"Thunderpuff",0,0,512,1,0.3,0)
		THUN C 0 A_GunFlash
		THUN C 0 A_ReFire
		THUN A 0 A_StopSound(7)
		THUN C 0 A_PlaySound("weapons/flamethrower/end",6)
		THUN BA 2
		goto Ready
	InWater:
		THUN A 0 A_StopSound(7)
		THUN C 1 A_SpawnItemEx("ThunderboltWhoopsie",random(0,8),random(0,8),random(10,30),0,0,0,random(0,360))
		TNT1 A 0 A_JumpIf(Health > 0,"InWater")
		Goto Deselect
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		Goto LightDone
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		THUX A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

actor Thunderpuff : PuffBase
{
	damagetype Lightning
}

actor ThunderboltWhoopsie
{
	Radius 1
	Height 1
	Speed 10
	//Damage 20
	PROJECTILE
	DeathSound "weapons/explode"
	Obituary "$OB_BIRTHDAYPARTY"
	scale 0.3
	damagetype Fire
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Explode(20,128,1)
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_PlaySound("weapons/explode",CHAN_VOICE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 8 A_SpawnItemEx("Spawner_Explosion_Normal1")
		stop
	}
}

// SUPERWEAPON: BFG 9000
ACTOR BFGAmmo : Ammo
{
	Inventory.Amount 5
	Inventory.MaxAmount 5
	Inventory.Icon "HUDSUPER"
	Tag "$TAGSUPER"
}

ACTOR BFG : Weapon
{
	Height 20
	Weapon.SelectionOrder 2800
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "BFGAmmo"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTBFG"
	Tag "$TAG_BFG9000"
	Inventory.PickupSound "misc/ws_pkup"
	States
	{
	Ready:
		BFGG A 1 A_WeaponReady
		Loop
	Deselect:
		BFGG A 0 A_StopSound(6)
		BFGG A 0 A_PlaySound("weapons/in")
		//BFGG A 0 A_Lower
		BFGG A 1 A_Lower
		goto Deselect+2
	Select:
		BFGG A 0 A_StopSound(6)
		BFGG A 0 A_PlaySound("weapons/out")
		//BFGG A 0 A_Raise
		BFGG A 1 A_Raise
		goto Select+2
	Fire:
		BFGG A 0 A_PlaySound("weapons/bfgf", CHAN_WEAPON)
		BFGG DEDEDEDEDEDAEADAEADA 1
		BFGG A 2 A_GunFlash
		BFGN A 1 Offset(0,32)
		BFGN A 1 Offset(0,33)
		BFGN A 1 Offset(0,34)
		BFGN A 1 Offset(0,35)
		BFGN A 1 Offset(0,37)
		BFGN A 1 Offset(0,39)
		BFGN A 1 Offset(0,43)
		BFGN A 0 A_FireCustomMissile("DoomBFGBall")
		BFGN A 1 Offset(0,45)
		BFGN A 1 Offset(0,47)
		BFGN A 1 Offset(0,48)
		BFGN A 1 Offset(0,49)
		BFGN A 1 Offset(0,48)
		BFGN A 1 Offset(0,47)
		BFGN A 1 Offset(0,45)
		BFGN A 1 Offset(0,43)
		BFGN A 1 Offset(0,39)
		BFGN A 1 Offset(0,37)
		BFGN A 1 Offset(0,35)
		BFGN A 1 Offset(0,34)
		BFGN A 1 Offset(0,33)
		BFGN A 1 Offset(0,32)
		BFGG A 25
		goto Ready
	Flash:
		BFGW A 2 bright
		BFGX HGFE 1 bright A_Light2
		BFGX DCBA 1 bright A_Light2
		TNT1 A 0 A_Light0
		BFGY ABCDE 1 bright
		BFGY FGHIJKLLLL 1
		Goto LightDone
	Spawn:
		//TNT1 A 0
		//TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		//TNT1 A 0 A_ChangeFlag("DROPPED",0)
		BFUG A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}


ACTOR DoomBFGBall
{
	Radius 13
	Height 8
	Speed 25
	Damage 100
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	Obituary "$OB_MPBFG_BOOM"
	States
	{
	Spawn:
		TNT1 AAA 0 A_CustomMissile("TelefogParticle",random(4,16),0,random(0,360),2,random(4,32))
		BFS1 A 4 Bright
		TNT1 AAA 0 A_CustomMissile("TelefogParticle",random(4,16),0,random(0,360),2,random(4,32))
		BFS1 B 4 Bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound("weapons/bfgx")
		BFE1 AB 4 Bright
		TNT1 AAAAAAA 0 A_CustomMissile("TelefogParticle",random(4,16),0,random(0,360),2,random(4,32))
		BFE1 C 4 Bright A_BFGSpray("DoomBFGExtra")
		BFE1 DEF 4 Bright
		Stop
	}
}

ACTOR DoomBFGExtra
{
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.75
	DamageType "BFGSplash"
	States
	{
	Spawn:
		TNT1 A 0 A_PlaySound("weapons/bfgx")
		TNT1 AAAAAAA 0 A_CustomMissile("TelefogParticle",random(4,16),0,random(0,360),2,random(4,32))
		BFE2 ABCD 4 Bright
		Stop
	}
}