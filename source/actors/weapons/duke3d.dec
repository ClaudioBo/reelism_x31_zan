//Splot - Duke

ACTOR Duke3DRPG : Weapon
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "DukeRPGAmmo"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTRPG"
	Tag "$TAGRPG"
	Weapon.BobStyle Smooth
	damagetype DukeRPG
	States
	{
	Ready:
		DRPG A 1 A_WeaponReady
		Loop
	Deselect:
		DRPG A 0 A_PlaySound("weapons/in")
		//DRPG A 0 A_Lower
		DRPG A 1 A_Lower
		goto Deselect+1
	Select:
		DRPG A 0 A_PlaySound("weapons/out")
		DRPG A 0 A_PlaySound("duke/wpnsel")
		//DRPG A 0 A_Raise
		DRPG A 1 A_Raise
		goto Select+2
	Fire:
		DRPG B 0 A_FireCustomMissile("Duke3DMissile",0,1,12,-12)
		BAZO D 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FireFast")
		DRPG BCDEFGHIJKLMNOPQRS 1
		Goto FireFinish
	FireFast:
		DRPG BDFHJLNPR 2
		Goto FireFinish
	FireFinish:
		DRPG A 15 A_WeaponReady(WRF_NOFIRE)
		BAZO A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 3 A_Light1
		TNT1 B 4
		TNT1 CD 4 A_Light2
		Goto LightDone
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		DRPG Z 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

actor Duke3DMissile
{
	Radius 11
	Height 8
	Speed 30
	Damage (80)
	scale 0.4
	Projectile
	+RANDOMIZE
	+BLOODSPLATTER
	//+FORCEXYBILLBOARD
	SeeSound "duke/rpgfire"
	DeathSound "duke/bombexp"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	damagetype DukeRPG
	States
	{
	Spawn:
		RPGX A 1
		RPGX A 1 bright A_SpawnItemEx("Duke3DMissilePart")
		loop
	Death:
		TNT1 A 0 A_Explode(140,98)
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 0 A_SpawnItemEx("Spawner_Explosion_Normal2")
		TNT1 A 0 A_SetScale(1)
		DEXP ABCDEFGHIJKLMNOPQRSTU 2 BRIGHT
		stop
	}
}

Actor Duke3DMissilePart
{
	Height 1
	Radius 1
	+NoGravity
	+NoBlockMap
	//+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	VSpeed 1
	States
	{
	Spawn:
		TNT1 A 2
		DKPF ABCD 4
		Stop
	}
}

ACTOR Duke3DRipper : Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoType "DukeChaingunAmmo"
	Tag "$TAGRIPPER"
	Weapon.SelectionOrder 1700
	Weapon.AmmoGive 50
	Obituary "%o was mowed down by %k's chaingun cannon."
	Inventory.PickupMessage "$GOTRIPPER"
	Attacksound "duke/ripper"
	Weapon.BobStyle Smooth
	Scale .7
	States
	{
	Ready:
		NCHG A 1 A_WeaponReady
		Loop
	Deselect:
		NCHG A 0 A_PlaySound("weapons/in")
		//NCHG A 0 A_Lower
		NCHG A 1 A_Lower
		goto Deselect+1
	Select:
		NCHG A 0 A_PlaySound("weapons/out")
		NCHG A 0 A_PlaySound("duke/wpnsel")
		//NCHG A 0 A_Raise
		NCHG A 1 A_Raise
		goto Select+2
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FireFast")
		NCHG B 1 BRIGHT A_GunFlash
		NCHG B 1 BRIGHT Offset(1,34)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(5.6,0,1,9+random(0,5),"HandgunPuff",FBF_NORANDOM|FBF_USEAMMO)
		NCHG C 1 BRIGHT Offset(3,33) A_FireCustomMissile("BulletCasingSpawn",-45,0,1,8)
		NCHG C 1 BRIGHT Offset(2,32)
		NCHG D 1 BRIGHT Offset(3,34)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(5.6,0,1,9+random(0,5),"HandgunPuff",FBF_NORANDOM|FBF_USEAMMO)
		NCHG D 1 BRIGHT Offset(1,32) A_FireCustomMissile("BulletCasingSpawn",-45,0,1,8)
		NCHG E 1 BRIGHT Offset(3,33)
		NCHG E 1 BRIGHT Offset(2,34)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(5.6,0,1,9+random(0,5),"HandgunPuff",FBF_NORANDOM|FBF_USEAMMO)
		NCHG A 1 BRIGHT Offset(1,33) A_FireCustomMissile("BulletCasingSpawn",-45,0,1,8)
		TNT1 A 0 A_ReFire
		Goto Ready
	FireFast:
		NCHG B 1 BRIGHT A_GunFlash
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(5.6,0,1,9+random(0,5),"HandgunPuff",FBF_NORANDOM|FBF_USEAMMO)
		NCHG C 1 BRIGHT Offset(3,33) A_FireCustomMissile("BulletCasingSpawn",-45,0,1,8)
		NCHG D 1 BRIGHT Offset(3,34)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(5.6,0,1,9+random(0,5),"HandgunPuff",FBF_NORANDOM|FBF_USEAMMO)
		NCHG E 1 BRIGHT Offset(3,33) A_FireCustomMissile("BulletCasingSpawn",-45,0,1,8)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(5.6,0,1,9+random(0,5),"HandgunPuff",FBF_NORANDOM|FBF_USEAMMO)
		NCHG A 1 BRIGHT Offset(1,33) A_FireCustomMissile("BulletCasingSpawn",-45,0,1,8)
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		Goto LightDone
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		NCHG Z 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

ACTOR Duke3DFreezethrower : Weapon
{
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoType "DukeIceAmmo"
	Weapon.AmmoType2 "DukeIceAmmo"
	Tag "Freezethrower"
	Weapon.SelectionOrder 1600
	Weapon.AmmoGive 25
	Inventory.PickupMessage "$GOTFREEZETHROWER"
	Weapon.BobStyle Smooth
	+WEAPON.ALT_AMMO_OPTIONAL
	Scale .7
	States
	{
	Ready:
		NFRZ A 1 A_WeaponReady
		Loop
	Deselect:
		NFRZ A 0 A_PlaySound("weapons/in")
		//NFRZ A 0 A_Lower
		NFRZ A 1 A_Lower
		goto Deselect+1
	Select:
		NFRZ A 0 A_PlaySound("weapons/out")
		NFRZ A 0 A_PlaySound("duke/wpnsel")
		//NFRZ A 0 A_Raise
		NFRZ A 1 A_Raise
		goto Select+2
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FireFast")
		NFRZ B 1 BRIGHT A_GunFlash
		NFRZ B 1 BRIGHT Offset(1,34)
		NFRZ C 1 BRIGHT Offset(3,33)
		NFRZ C 1 BRIGHT Offset(2,32)
		TNT1 A 0 A_FireCustomMissile("DukeFreezeShot")
		NFRZ D 1 BRIGHT Offset(3,34)
		TNT1 A 0 A_ReFire
		Goto Ready
	FireFast:
		NFRZ B 2 BRIGHT A_GunFlash
		NFRZ C 2 BRIGHT Offset(3,33)
		TNT1 A 0 A_FireCustomMissile("DukeFreezeShot")
		NFRZ D 1 BRIGHT Offset(3,34)
		TNT1 A 0 A_ReFire
		Goto Ready
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		TNT1 A 0 A_ChangeFlag("DROPPED",0)
		NFRZ Z 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Flash:
		TNT1 A 1
		Goto LightDone
	AltFire:
		TNT1 A 0 A_PlaySound("attack/punchmiss")
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"AltFireFast")
		KICK BCDEF 1
		KICK G 1 A_CustomPunch(10,0,0,"FistPuff")
		KICK HIJJJJJJJJJJJFEDCBA 1
		Goto Ready
	AltFireFast:
		KICK BDF 2
		KICK G 2 A_CustomPunch(10,0,0,"FistPuff")
		KICK IJJJJJFDB 2
		Goto Ready
	}
}
ACTOR DukeFreezeShot
{
	Radius 13
	Height 8
	Speed 40
	Damage (random(21,25))
	Projectile
	-BLOODSPLATTER
	DamageType Ice
	Scale .15
	BounceType Hexen
	BounceCount 3
	+NODAMAGETHRUST
	+SPAWNSOUNDSOURCE
	SeeSound "duke/freezer"
	Obituary "%k put %o on ice."
	+SPECTRAL
	States
	{
	Spawn:
		NFZS ABC 2
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("DukeFreezeSparkle")
		Stop
	}
}
ACTOR DukeFreezeSparkle
{
	+CLIENTSIDEONLY
	Scale .5
	+NOGRAVITY
	States
	{
	Spawn:
		NFZB ABCDEF 1
		Stop
	}
}

// SUPERWEAPON - Devastator
ACTOR Duke3DDevastatorAmmo : Ammo
{
	Inventory.Amount 50
	Inventory.MaxAmount 50
	Inventory.Icon "HUDSUPER"
	Tag "$TAGSUPER"
}

actor DukeDevastatorFiringSequence : Inventory{+INVENTORY.UNDROPPABLE}

actor Duke3DDevastator : Weapon
{
	Weapon.SelectionOrder 2800
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "Duke3DDevastatorAmmo"
	Inventory.Pickupmessage "$GOTDEV"
	Tag "$TAGDEV"
	Inventory.PickupSound "misc/ws_pkup"
	Scale 0.9
	+NOALERT
	+INVENTORY.UNDROPPABLE
	Weapon.BobStyle Smooth
	States
	{
	Spawn:
		//TNT1 A 0
		//TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(902)==5||ACS_ExecuteWithResult(904)>0,2)
		//TNT1 A 0 A_ChangeFlag("DROPPED",0)
		DEVP A 1050 bright
		TNT1 A 0 A_SpawnItemEx("DespawnCloudPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	Ready:
		DDEV A 1 A_WeaponReady
		Loop
	Deselect:
		DDEV A 0 A_PlaySound("weapons/in")
		//DDEV A 0 A_Lower
		DDEV A 1 A_Lower
		goto Deselect+1
	Select:
		DDEV A 0 A_PlaySound("weapons/out")
		DDEV A 0 A_PlaySound("duke/wpnsel")
		//DDEV A 0 A_Raise
		DDEV A 1 A_Raise
		goto Select+2
	Fire:
	FireRight:
		TNT1 A 0 A_JumpIfInventory("DukeDevastatorFiringSequence",1,"FireLeft")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("DukeDevastatorFiringSequence",1)
		DDEV H 1 A_WeaponReady(12)
		TNT1 A 0 A_FireCustomMissile("DukeDevastatorRocket",random(-3,3),1,5,0,1,random(-1,1))
		DDEV I 2 A_WeaponReady(12)
		DDEV J 1 BRIGHT A_WeaponReady(12)
		TNT1 A 0 A_FireCustomMissile("DukeDevastatorRocket",random(-3,3),1,6,2,1,random(-1,1))
		DDEV J 1 BRIGHT A_WeaponReady(12)
		DDEV KLMA 1 BRIGHT A_WeaponReady(12)
		TNT1 A 0 A_ReFire
		Goto Ready
	FireLeft:
		TNT1 A 0 A_TakeInventory("DukeDevastatorFiringSequence",1)
		TNT1 A 0 A_AlertMonsters
		DDEV B 1 A_WeaponReady(12)
		TNT1 A 0 A_FireCustomMissile("DukeDevastatorRocket",random(-3,3),1,-5,0,1,random(-1,1))
		DDEV C 2 A_WeaponReady(12)
		DDEV D 1 BRIGHT A_WeaponReady(12)
		TNT1 A 0 A_FireCustomMissile("DukeDevastatorRocket",random(-3,3),1,-6,2,1,random(-1,1))
		DDEV D 1 BRIGHT A_WeaponReady(12)
		DDEV EFGA 1 BRIGHT A_WeaponReady(12)
		TNT1 A 0 A_ReFire
		Goto Ready
	}
}

actor DukeDevastatorRocket
{
	Radius 3
	Height 2
	Speed 30
	Damage (38)
	Projectile
	Scale 0.2
	DamageType DukeRPG
	SeeSound "duke/devfire"
	DeathSound "duke/bombexp"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		RPGX A 1
		loop
	Death:
		TNT1 A 0 A_Explode(35,24)
		TNT1 A 0 A_PlaySound("weapons/explodedist",CHAN_WEAPON,1,0,ATTN_NONE)
		TNT1 A 0 A_Quake(6,8,0,2048,"weapons/explodedist")
		TNT1 A 0 A_SpawnItemEx("Spawner_Explosion_Normal2")
		TNT1 A 0 A_SetScale(0.3)
		DEXP ABCDEFGHIJKLMNOPQRSTU 2 BRIGHT
		Stop
	}
}