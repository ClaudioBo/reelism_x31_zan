// Splot - Tank Morph

ACTOR TankPlayer : PlayerPawn
{
	Health 999
	Height 100
	Mass 999999
	PainChance 0
	MaxStepHeight 32
	Player.DisplayName "TAAANK!"
	Player.MorphWeapon "TankWeapon"
	Player.SideMove 0
	Player.ViewHeight 120
	Player.ForwardMove 0.5,0.5
	Player.JumpZ 0
	Player.AttackZOffset 64
	DamageFactor 0.05
	DamageFactor RunOver,0
	//+NODAMAGE
	+NOSKIN
	-PICKUP
	//+FORCEXYBILLBOARD
	bloodtype "MetalSpark"
	+NOBLOODDECALS
	Player.SoundClass Tank
	States
	{
	Spawn:
		TANK A 1 A_SpawnItemEx("TankTreads")
		Loop
	See:
		TANK A 0 A_PlaySound("tank/move",7)
		TANK A 0 A_SpawnItemEx("TankTreads")
		TANK A 8 A_Quake(1,8,0,64,"null")
		Goto Spawn
	Death:
		TANK A -1
		Stop
	}
}
ACTOR TankTreads
{
	DamageType RunOver
	+NODAMAGETHRUST
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(1000,100,0,1,100)
		Stop
	}
}
ACTOR TankTreads128 : TankTreads	//For Imp Tank Boss
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(1000,128,0,1,128)
		Stop
	}
}

actor TankWeapon : Weapon
{
	+WEAPON.DONTBOB
	+INVENTORY.UNDROPPABLE
	//+INVENTORY.UNTOSSABLE
	Tag "$TAGTANK"
	States
	{
	Ready:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("TankCooldown",1,3)
		TNT1 A 0 A_JumpIfInventory("TankCooldownFast",1,2)
		TWEP A 1 A_WeaponReady
		Loop
		TWEP A 1 A_WeaponReady(WRF_NOPRIMARY)
		Loop
	Deselect:
		TWEP A 1 A_Lower
		Loop
	Select:
		TWEP A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_Quake(9,8,0,128,"null")
		TNT1 A 0 A_PlaySound("weapons/tankmissile/fire",5)
		TWEP B 2 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("TankRocket",0,0,0,0)
		TNT1 A 0 ThrustThing(angle*256/360+128,8,0,0)
		TWEP BBBAA 1 A_SetPitch(pitch-2)
		TWEP AAAAAAAAAA 1 A_SetPitch(pitch+1)
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
		TWEP A 0 A_GiveInventory("TankCooldown")
		Goto Ready
		TWEP A 0 A_GiveInventory("TankCooldownFast")
		Goto Ready
	AltFire:
		TNT1 A 0 A_Quake(8,2,0,128,"null")
		TNT1 A 0 A_PlaySound("weapons/superm60",CHAN_WEAPON)
		TNT1 A 0 A_FireBullets(8,8,1,15,"HandgunPuff")
		TNT1 A 0 A_FireCustomMissile("BulletCasingSpawn",5,0,16,8)
		TWEP C 2 A_GunFlash
		TWEP A 2
		TWEP A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 1 A_Light2
		Goto LightDone
	}
}

actor TankRocket : Missile
{
	Speed 120
	Damage 250
	DeathSound "weapons/tankmissile/explode"
}

actor BossTankRocket : Missile
{
	Speed 80
	DeathSound "weapons/tankmissile/explode"
	States
	{
	Death:
		TNT1 A 0 A_Explode(128,128,0)
		Goto Super::Death+1
	}
}

ACTOR TankCooldown : PowerProtection
{
	Powerup.Duration 64
}
ACTOR TankCooldownFast : TankCooldown
{
	Powerup.Duration 32
}

// HYPERTANK!
ACTOR HyperTankPlayer : TankPlayer
{
	Player.DisplayName "HYPER-TANK"
	Player.MorphWeapon "HyperTankWeapon"
	Player.ForwardMove 1.25,1.25
}

actor HyperTankWeapon : TankWeapon
{
	States
	{
	Ready:
		TNT1 A 0 //A_SetRoll(0)
		TNT1 A 0 A_StopSound(6)
		HWEP A 1 A_WeaponReady
		Loop
	Fire:
		HWEP BA 1 A_GunFlash
		TNT1 A 0 A_PlaySound("tank/hloop",6,1,1)
		HWEP A 0 A_ReFire
	Hold:
		TNT1 A 0 A_SetPitch(pitch+random(-2,2))
		TNT1 A 0 A_SetAngle(angle+random(-2,2))
		TNT1 A 0 //A_SetRoll(random(-4,4))
		TNT1 A 0 A_Quake(6,8,0,128,"null")
		TNT1 A 0 A_PlaySound("tank/hlaser",5)
		TNT1 AA 0 A_FireCustomMissile("TankPulsar1",random(-8,8),0,random(-4,4),random(-8,0),0,random(-5,5))
		TNT1 AA 0 A_FireCustomMissile("TankPulsar2",random(-8,8),0,random(-4,4),random(-8,0),0,random(-5,5))
		TNT1 AA 0 A_FireCustomMissile("TankPulsar3",random(-8,8),0,random(-4,4),random(-8,0),0,random(-5,5))
		TNT1 A 0 ThrustThing(angle*256/360+128,3,0,0)
		HWEP BA 1 A_GunFlash
		HWEP A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_Quake(8,2,0,128,"null")
		TNT1 A 0 A_PlaySound("weapons/tankmissile/hyper",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("HyperTankRocket",0,0,random(32,40),random(-20,0))
		TNT1 A 0 ThrustThing(angle*256/360+128,3,0,0)
		HWEP C 2 A_GunFlash // should be c
		HWEP A 3
		HWEP A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 1 A_Light2
		Goto LightDone
	}
}

actor HyperTankRocket : TankRocket
{
	Damage 125
}

actor TankPulsar1
{
	Radius 12
	Height 12
	Speed 80
	Scale 1.25
	Damage (100)
	PROJECTILE
	+BRIGHT
	DeathSound "weapons/plasmax"
	states
	{
	Spawn:
		PLS2 AAA 3 A_SpawnItemEx("TankPulsarTrail1")
		PLS2 BBB 3 A_SpawnItemEx("TankPulsarTrail1")
		Loop
	Death:
		PLS2 CDEFG 4
		Stop
	}
}

actor TankPulsar2 : TankPulsar1
{
	Speed 70
	states
	{
	Spawn:
		HTPA AAA 3 A_SpawnItemEx("TankPulsarTrail2")
		HTPA BBB 3 A_SpawnItemEx("TankPulsarTrail2")
		Loop
	}
}

actor TankPulsar3 : TankPulsar1
{
	Speed 60
	states
	{
	Spawn:
		HTPB AAA 3 A_SpawnItemEx("TankPulsarTrail3")
		HTPB BBB 3 A_SpawnItemEx("TankPulsarTrail3")
		Loop
	}
}

Actor TankPulsarTrail1 : BulletPuff
{
	VSpeed 0
	RenderStyle Normal
	Alpha 0.9
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		PLS2 AB 3 bright A_FadeOut(.15,1)
		loop
	}
}

Actor TankPulsarTrail2 : TankPulsarTrail1
{
	States
	{
	Spawn:
		HTPA AB 3 bright A_FadeOut(.15,1)
		loop
	}
}

Actor TankPulsarTrail3 : TankPulsarTrail1
{
	States
	{
	Spawn:
		HTPB AB 3 bright A_FadeOut(.15,1)
		loop
	}
}