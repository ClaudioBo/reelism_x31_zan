// Splot - Imp Variants
ACTOR AdultImp : MonsterTemplate replaces DoomImp
{
	Health 60
	Height 56
	Mass 100
	Speed 8
	PainChance 200
	SeeSound "imp/see"
	PainSound "imp/hurt"
	DeathSound "imp/die"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	States
	{
	Spawn:
		TROO A 0
		"####" A 0 Thing_ChangeTID(0,900)
		"####" A 1 A_Look
		"####" A 0 A_AlertMonsters
		goto See
	See:
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==9,"DogWhistle")
		//"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" B 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" B 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" C 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" C 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" D 4 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" D 4 A_Chase
		Loop
	MidAir:
		"####" DDDDDD 1 A_CheckFloor("See")
		"####" A 0 A_SpawnItemEx("CrushDamage")
		"####" D 1 A_CheckFloor("Landing")
		"####" A 0 A_JumpIf(velz==0,"See")
		goto MidAir+6
	Landing:
		"####" E 1
		"####" E 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" E 5 A_PlaySound("human/land")
		Goto See
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_CustomComboAttack("AdultImpBall",16,3,"imp/melee","Enemy")
		Goto See
	Pain:
		"####" H 2
		"####" H 2 A_Pain
		Goto See
// NORMAL DEATH STATES
	Death:
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==4,"XDeath")
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		"####" H 0 A_NoBlocking
		"####" I 5 A_GiveToTarget("ScoreItem",100)
		"####" J 4 A_Scream
		"####" KL 3
		goto CorpseFalling
	CorpseFalling:
		"####" KLKLKL 1 A_CheckFloor("Corpse")
		"####" K 1 A_CheckFloor("CorpseHard")
		"####" A 0 A_JumpIf(velz==0,"Corpse")
		goto CorpseFalling+6
	Corpse:
		"####" M 1 A_PlaySound("human/body")
		"####" M 1 A_JumpIf(velz!=0,"CorpseFalling")
		"####" A 0 A_CheckSight("Cleanup")
		goto Corpse+1
	CorpseHard:
		"####" M 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto Corpse
	// GIB DEATH STATES
	XDeath:
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		"####" N 0 A_NoBlocking
		"####" N 0 A_GiveToTarget("ScoreItem",130)
		"####" A 0 A_StopSound(CHAN_VOICE)
		"####" N 0 A_PlaySound("human/gib")
		"####" N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		"####" N 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	// COMEDY DEATH STATES
	Death.Explosive:
		"####" F 0 A_Jump(64,"XDeath")
		"####" F 0 A_GiveToTarget("ScoreItem",100)
		"####" F 0 A_NoBlocking
		"####" F 0 ThrustThingZ(0,random(32,128),0,1)
		"####" F 0 A_Recoil(random(16,48))
		"####" K 2 A_Scream
		goto CorpseFlying
	CorpseFlying:
		"####" K 0 A_GiveToTarget("ScoreItem",1)
		"####" G 0 A_SpawnItemEx("BloodShredSilent")
		"####" G 0 A_SpawnItemEx("MissilePart",0,0,8)
		"####" G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		"####" L 1 A_CheckFloor("CorpseCrater")
		"####" F 0 A_GiveToTarget("ScoreItem",1)
		"####" H 0 A_SpawnItemEx("BloodShredSilent")
		"####" H 0 A_SpawnItemEx("MissilePart",0,0,8)
		"####" G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		"####" K 1 A_CheckFloor("CorpseCrater")
		loop
	CorpseCrater:
		"####" M 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		"####" N 0 A_PlaySound("human/gib")
		"####" F 0 A_GiveToTarget("ScoreItem",50)
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		"####" N 0 A_Stop
		goto Corpse+1
	Death.Fire:
		"####" A 0 A_PlaySound("human/scream")
		"####" A 0 A_SpawnItemEx("Flame1A",0,0,32)
		"####" A 0 A_Recoil(-8)
		"####" A 0 A_SpawnItemEx("Ember1",0,0,32)
		"####" AAAA 1 A_SetAngle(12 + angle)
		"####" B 0 A_Recoil(-8)
		"####" A 0 A_SpawnItemEx("Flame2A",0,0,32)
		"####" BBBB 1 A_SetAngle(12 + angle)
		"####" C 0 A_Recoil(-8)
		"####" A 0 A_SpawnItemEx("Flame1A",0,0,32)
		"####" CCCC 1 A_SetAngle(12 + angle)
		"####" D 0 A_Recoil(-8)
		"####" DDDD 1 A_SetAngle(12 + angle)
		"####" A 0 A_SpawnItemEx("Flame3B",0,0,32)
		"####" A 0 A_Jump(128,"Death.Fire")
		goto Death
	DogWhistle:
		"####" A 0 A_SpawnItemEx("Confusion",0,0,56)
		//"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" B 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" B 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 0 A_SpawnItemEx("Confusion",0,0,56)
		"####" C 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" C 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" D 2 A_Wander
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" D 2 A_Wander
		goto See
	}
}

ACTOR AdultImpBall : DoomImpBall
{
	SeeSound "imp/fire"
	damagetype Enemy
}

// Juvenile Imp
ACTOR JuvenileImp : AdultImp
{
	Health 40
	Radius 15
	Height 50
	Mass 80
	scale 0.7
	Speed 12
	MaxStepHeight 32
	MaxDropoffHeight 32
	SeeSound "jmp/see"
	PainSound "jmp/hurt"
	DeathSound "jmp/die"
	HitObituary "$OB_JIMPHIT"
	Obituary "$OB_JIMP"
	States
	{
	Spawn:
		TRO3 A 0
		Goto Super::Spawn+1
	Melee:
	Missile:
		TRO3 EF 8 A_FaceTarget
		TRO3 G 6 A_CustomComboAttack("JuvenileImpBall",16,2,"imp/melee","Enemy")
		Goto See
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,48)
		//TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 A 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 A 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 B 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 B 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,48)
		TRO3 C 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 C 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 D 2 A_Wander
		TRO3 A 0 A_JumpIf(velz!=0,"MidAir")
		TRO3 D 2 A_Wander
		goto See
	}
}

ACTOR JuvenileImpBall : AdultImpBall
{
	Speed 15
	FastSpeed 25
	Damage 2
	scale 0.7
	SeeSound "jmp/fire"
	DeathSound "imp/shotx"
}

ACTOR SeniorImp : AdultImp
{
	Health 80
	Height 59
	Speed 7
	scale 1.1
	SeeSound "amp/see"
	PainSound "amp/hurt"
	DeathSound "amp/die"
	HitObituary "$OB_BIGIMPHIT"
	Obituary "$OB_BIGIMP"
	States
	{
	Spawn:
		TRO4 A 0
		Goto Super::Spawn+1
	Melee:
	Missile:
		TRO4 EF 4 A_FaceTarget
		TRO4 G 3 A_CustomComboAttack("JuvenileImpBall",32,3,"imp/melee","Enemy")
		TRO4 FEF 4 A_FaceTarget
		TRO4 G 3 A_CustomComboAttack("AdultImpBall",32,3,"imp/melee","Enemy")
		TRO4 FEF 4 A_FaceTarget
		TRO4 G 3 A_CustomComboAttack("SeniorImpBall",32,3,"imp/melee","Enemy")
		Goto See
	}
}

ACTOR SeniorImpBall : AdultImpBall
{
	Speed 7
	Damage 4
	scale 1.5
	SeeSound "amp/fire"
}