// Splot - LOTS OF DOGS
ACTOR Dog1 : MonsterTemplate
{
	Health 5
	GibHealth 50
	Speed 20
	PainChance 180
	Radius 12
	Height 56
	Mass 100
	//ActiveSound "dog/doggy"
	AttackSound "dog/attack"
	DeathSound "dog/die"
	PainSound "dog/pain"
	//SeeSound "dog/doggy"
	Obituary "$OB_DOG"
	DropItem "DynAmmo",64
	States
	{
	Spawn:
		DOGS A 0
		DOGS A 0 Thing_ChangeTID(0,900)
		DOGS A 1 A_Look
		DOGS A 0 A_AlertMonsters
		goto See
	See:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==9,"DogWhistle")
		//DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS A 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS A 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS B 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS B 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS C 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS C 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS D 2 A_Chase
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS D 2 A_Chase
		TNT1 A 0 A_PlaySound("dog/doggy")
		Loop
	MidAir:
		DOGS DDDDDD 1 A_CheckFloor("See")
		TNT1 A 0 A_SpawnItemEx("CrushDamage")
		DOGS D 1 A_CheckFloor("Landing")
		TNT1 A 0 A_JumpIf(velz==0,"See")
		goto MidAir+6
	Landing:
		DOGS C 1
		DOGS C 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		DOGS C 5 A_PlaySound("human/land")
		goto See
	Melee:
		DOGS EF 6 A_FaceTarget
		DOGS G 6 A_SargAttack
		Goto See
	Pain:
		DOGS H 2
		DOGS H 2 A_Pain
		Goto See
	// NORMAL DEATH STATES
	Death:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==4,"XDeath")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		DOGS I 0 A_NoBlocking
		DOGS I 5 A_GiveToTarget("ScoreItem",100)
		DOGS J 4 A_Scream
		DOGS KL 3
		goto CorpseFalling
	CorpseFalling:
		DOGS KLKLKL 1 A_CheckFloor("Corpse")
		DOGS K 1 A_CheckFloor("CorpseHard")
		TNT1 A 0 A_JumpIf(velz==0,"Corpse")
		goto CorpseFalling+6
	Corpse:
		DOGS M 1 A_PlaySound("human/body")
		DOGS N 1 A_JumpIf(velz!=0,"CorpseFalling")
		TNT1 A 0 A_CheckSight("Cleanup")
		goto Corpse+1
	CorpseHard:
		DOGS N 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto Corpse
	// GIB DEATH STATES
	XDeath:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BRNY E 0 A_NoBlocking
		BRNY E 0 A_GiveToTarget("ScoreItem",130)
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		BRNY E 0 A_PlaySound("human/gib")
		BRNY E 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		DOGS I 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	// COMEDY DEATH STATES
	Death.Explosive:
		BEGR F 0 A_Jump(64,"XDeath")
		BEGR F 0 A_GiveToTarget("ScoreItem",100)
		BEGR F 0 A_NoBlocking
		BEGR F 0 ThrustThingZ(0,random(32,128),0,1)
		BEGR F 0 A_Recoil(random(16,48))
		DOGS J 2 A_Scream
		goto CorpseFlying
	CorpseFlying:
		DOGS K 0 A_GiveToTarget("ScoreItem",1)
		BEGR G 0 A_SpawnItemEx("BloodShredSilent")
		BEGR G 0 A_SpawnItemEx("MissilePart",0,0,8)
		BEGR G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		DOGS K 1 A_CheckFloor("CorpseCrater")
		BEGR F 0 A_GiveToTarget("ScoreItem",1)
		BEGR H 0 A_SpawnItemEx("BloodShredSilent")
		BEGR H 0 A_SpawnItemEx("MissilePart",0,0,8)
		BEGR G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		DOGS J 1 A_CheckFloor("CorpseCrater")
		loop
	CorpseCrater:
		BEGR M 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		BEGR N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		BEGR N 0 A_PlaySound("human/gib")
		BEGR F 0 A_GiveToTarget("ScoreItem",50)
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BEGR N 0 A_Stop
		goto Corpse+1
	Death.Fire:
		DOGS A 0 A_PlaySound("dog/die")
		DOGS A 0 A_SpawnItemEx("Flame1A",0,0,32)
		DOGS A 0 A_Recoil(-8)
		DOGS A 0 A_SpawnItemEx("Ember1",0,0,32)
		DOGS AAAA 1 A_SetAngle(12 + angle)
		DOGS B 0 A_Recoil(-8)
		DOGS A 0 A_SpawnItemEx("Flame2A",0,0,32)
		DOGS BBBB 1 A_SetAngle(12 + angle)
		DOGS C 0 A_Recoil(-8)
		DOGS A 0 A_SpawnItemEx("Flame1A",0,0,32)
		DOGS CCCC 1 A_SetAngle(12 + angle)
		DOGS D 0 A_Recoil(-8)
		DOGS DDDD 1 A_SetAngle(12 + angle)
		DOGS A 0 A_SpawnItemEx("Flame3B",0,0,32)
		DOGS A 0 A_Jump(128,"Death.Fire")
		goto Death
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		//DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS A 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS A 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS B 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS B 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS C 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS C 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS D 1 A_Wander
		DOGS A 0 A_JumpIf(velz!=0,"MidAir")
		DOGS D 1 A_Wander
		TNT1 A 0 A_PlaySound("dog/doggy")
		goto See
	Death.Barkblast:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(905) ==91310,"Dogpot")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("ScoreItem",50)
		TNT1 A 0 A_PlaySound("human/gib")
		TNT1 A 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		TNT1 A 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	Dogpot:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("ScoreItem",500)
		TNT1 A 0 A_PlaySound("human/gib")
		TNT1 A 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		TNT1 A 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS O 0 A_SpawnItemEx("DogSoul")
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	}
}