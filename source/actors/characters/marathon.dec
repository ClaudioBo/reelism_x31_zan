// Splot - Marathon

ACTOR Bob : MonsterTemplate
{
	Health 50
	Height 56
	Speed 3
	PainChance 200
	scale 0.43
	SeeSound "bob/gab"
	PainSound "bob/pain"
	DeathSound "bob/die"
	ActiveSound "bob/gab"
	Obituary "$OB_BOB"
	+FRIENDLY
	States
	{
	Spawn:
		BAWB A 0
		BAWB A 0 Thing_ChangeTID(0,900)
		BAWB E 1 A_Look
		BAWB A 0 A_AlertMonsters
		goto See
	See:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==9,"DogWhistle")
		BAWB AAA 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB AAA 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB BBB 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB BBB 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB CCC 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB CCC 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB DDD 1 A_Chase
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB DDD 1 A_Chase
		Loop
	MidAir:
		BAWB DDDDDD 1 A_CheckFloor("See")
		TNT1 A 0 A_SpawnItemEx("CrushDamage")
		BAWB D 1 A_CheckFloor("Landing")
		TNT1 A 0 A_JumpIf(velz==0,"See")
		goto MidAir+6
	Landing:
		BAWB E 1
		POSS E 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		BAWB E 5 A_PlaySound("human/land")
		goto See
	Missile:
		BAWB EF 5 A_FaceTarget
		BAWB F 0 A_PlaySound("bob/attack")
		BAWB F 0 A_SpawnItemEx("BulletCasingSpawn",0,0,30)
		BAWB G 5 A_CustomBulletAttack(22.5,22.5,1,random(1,5) * 3,"EnemyHandgunPuff",3000,CBAF_NORANDOM)
		BAWB F 8
		Goto See
	Pain:
		BAWB H 3
		BAWB H 3 A_Pain
		Goto See
	// NORMAL DEATH STATES
	Death:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==4,"XDeath")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BAWB I 0 A_NoBlocking
		BAWB I 0 A_GiveToTarget("ScoreItem",100)
		BAWB I 5 A_Scream
	DeathLoopWait:
		BAWB I 1
		BAWB I 1 A_JumpIf(velx <= 3 && vely <= 3 && velz == 0, "FinishDying")
		loop
	FinishDying:
		BAWB I 4
		BAWB JKLMN 3
		goto Corpse
	CorpseFalling:
		BAWB NNNNNN 1 A_CheckFloor("Corpse")
		BAWB N 1 A_CheckFloor("CorpseHard")
		TNT1 A 0 A_JumpIf(velz==0,"Corpse")
		goto CorpseFalling+6
	Corpse:
		BAWB O 1 A_PlaySound("human/body")
		BAWB O 1 A_JumpIf(velz!=0,"CorpseFalling")
		TNT1 A 0 A_CheckSight("Cleanup")
		goto Corpse+1
	CorpseHard:
		BAWB O 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto Corpse
	// GIB DEATH STATES
	XDeath:
		POSS M 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		POSS M 0 A_GiveToTarget("ScoreItem",130)
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		POSS M 0 A_PlaySound("human/gib")
		POSS M 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		BAWB O 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		TNT1 A 0 A_SetScale(1)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	// COMEDY DEATH STATES
	Death.Explosive:
		BEGR F 0 A_Jump(64,"XDeath")
		BEGR F 0 A_GiveToTarget("ScoreItem",100)
		TNT1 A 0 A_NoBlocking
		BEGR F 0 ThrustThingZ(0,random(32,128),0,1)
		BEGR F 0 A_Recoil(random(16,48))
		BAWB O 2 A_Scream
		goto Death
	CorpseFlying:
		goto Death
	Death.Fire:
		POSS A 0 A_PlaySound("bob/burn")
	Burny:
		POSS A 0 A_SpawnItemEx("Flame1A",0,0,32)
		POSS A 0 A_Recoil(-8)
		POSS A 0 A_SpawnItemEx("Ember1",0,0,32)
		BAWB AAAA 1 A_SetAngle(12 + angle)
		POSS B 0 A_Recoil(-8)
		POSS A 0 A_SpawnItemEx("Flame2A",0,0,32)
		BAWB BBBB 1 A_SetAngle(12 + angle)
		POSS C 0 A_Recoil(-8)
		POSS A 0 A_SpawnItemEx("Flame1A",0,0,32)
		BAWB CCCC 1 A_SetAngle(12 + angle)
		POSS D 0 A_Recoil(-8)
		BAWB DDDD 1 A_SetAngle(12 + angle)
		POSS A 0 A_SpawnItemEx("Flame3B",0,0,32)
		POSS A 0 A_Jump(128,"Burny")
		goto Death
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		//POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB AA 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB AA 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB BB 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB BB 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		BAWB CC 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB CC 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB DD 1 A_Wander
		POSS A 0 A_JumpIf(velz!=0,"MidAir")
		BAWB DD 1 A_Wander
		goto See
	}
}

actor PissyBob : Bob { -FRIENDLY } // for boss rush