ACTOR DoomBaronOfHell : MonsterTemplate
{
	Health 800
	Radius 24
	Height 64
	Mass 1000
	Speed 8
	PainChance 50
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	ActiveSound "baron/active"
	Obituary "$OB_BARON"
	HitObituary "$OB_BARONHIT"
	bloodtype "GreenBloodShred"
	BloodColor "0 ff 0"
	States
	{
	Spawn:
		BOSS A 0
		"####" A 0 Thing_ChangeTID(0,900)
		"####" A 1 A_Look
		"####" A 0 A_AlertMonsters
		goto See
	See:
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==9,"DogWhistle")
		//"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" A 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" B 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" B 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" C 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" C 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" D 3 A_Chase
		"####" A 0 A_JumpIf(velz!=0,"MidAir")
		"####" D 3 A_Chase
		Loop
	MidAir:
		"####" DDDDDD 1 A_CheckFloor("See")
		"####" A 0 A_SpawnItemEx("CrushDamage")
		"####" D 1 A_CheckFloor("Landing")
		"####" A 0 A_JumpIf(velz==0,"See")
		goto MidAir+6
	Landing:
		"####" E 1
		"####" E 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" E 5 A_PlaySound("human/land")
		goto See
	Melee:
	Missile:
		"####" EF 8 A_FaceTarget
		"####" G 8 A_CustomComboAttack("BOHBall",32,10,"imp/melee","Enemy",1)	//A_BruisAttack
		Goto See
	Pain:
		"####" H 2
		"####" H 2 A_Pain
		Goto See
	// NORMAL DEATH STATES
	Death:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BOSS H 0 A_NoBlocking
		BOSS H 7 A_GiveToTarget("ScoreItem",500)
		BOSS I 6 A_Scream
		BOSS JKL 5
		goto CorpseFalling
	Death.Barkblast:
		"####" A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		"####" H 0 A_NoBlocking
		"####" H 7 A_GiveToTarget("ScoreItem",50)
		"####" I 6 A_Scream
		"####" JKL 5
		goto CorpseFalling
	CorpseFalling:
		"####" KLKLKL 1 A_CheckFloor("Corpse")
		"####" K 1 A_CheckFloor("CorpseHard")
		"####" A 0 A_JumpIf(velz==0,"Corpse")
		goto CorpseFalling+6
	Corpse:
		"####" O 1 A_PlaySound("human/body")
		"####" O 1 A_JumpIf(velz!=0,"CorpseFalling")
		"####" A 0 A_CheckSight("Cleanup")
		goto Corpse+1
	CorpseHard:
		"####" O 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto Corpse
	// COMEDY DEATH STATES
	Death.Fire:
		"####" A 0 A_PlaySound("human/scream")
		"####" A 0 A_SpawnItemEx("Flame1A",0,0,32)
		"####" A 0 A_Recoil(-8)
		"####" A 0 A_SpawnItemEx("Ember1",0,0,32)
		"####" AAAA 1 A_SetAngle(12 + angle)
		"####" B 0 A_Recoil(-8)
		"####" A 0 A_SpawnItemEx("Flame2A",0,0,32)
		"####" BBBB 1 A_SetAngle(12 + angle)
		"####" C 0 A_Recoil(-8)
		"####" A 0 A_SpawnItemEx("Flame1A",0,0,32)
		"####" CCCC 1 A_SetAngle(12 + angle)
		"####" D 0 A_Recoil(-8)
		"####" DDDD 1 A_SetAngle(12 + angle)
		"####" A 0 A_SpawnItemEx("Flame3B",0,0,32)
		"####" A 0 A_Jump(128,"Death.Fire")
		"####" A 0 A_Jump(256,"Death")
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,64)
		//BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS A 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS A 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS B 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS B 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,64)
		BOSS C 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS C 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS D 2 A_Wander
		BOSS A 0 A_JumpIf(velz!=0,"MidAir")
		BOSS D 2 A_Wander
		goto See
	Death.Bat:
		"####" A 0 A_GiveToTarget("ScoreItem",20)
		"####" A 0 A_FaceTarget
		"####" A 0 A_Recoil(48)
		"####" A 0 ThrustThingZ(0,128,0,1)
		"####" A 0 A_NoBlocking
		"####" A 0 A_Jump(256,"Death")
	Death.Explosive:
		"####" A 0 A_GiveToTarget("ScoreItem",100)
		"####" A 0 A_FaceTarget
		"####" A 0 A_Recoil(random(16,48))
		"####" A 0 ThrustThingZ(0,random(32,128),0,1)
		"####" A 0 A_NoBlocking
		"####" A 0 A_Jump(256,"Death")
	Death.SuperShotgun:
		"####" A 0 A_JumpIfCloser(72, "Death.SuperShotgunClose")
	Death.EnemyShotgun:
	Death.Shotgun:
		"####" A 0 A_JumpIfCloser(72, "Death.ShotgunClose")
		"####" A 0 A_Jump(256,"Death")
	Death.ShotgunClose:
		"####" A 0 A_GiveToTarget("ScoreItem", 20)
		"####" A 0 A_FaceTarget
		"####" A 0 A_Recoil(15)
		"####" A 0 ThrustThingZ (0, 50, 0, 1)
		"####" A 0 A_NoBlocking
		"####" A 0 A_Jump(256,"Death")
	Death.SuperShotgunClose:
		"####" A 0 A_GiveToTarget ("ScoreItem", 20)
		"####" A 0 A_FaceTarget
		"####" A 0 A_Recoil(100)
		"####" A 0 ThrustThingZ (0, 50, 0, 1)
		"####" A 0 A_NoBlocking
		"####" A 0 A_Jump(256,"Death")
	}
}

ACTOR DoomHellKnight : DoomBaronOfHell replaces HellKnight
{
	Health 300
	scale 0.9
	SeeSound "knight/sight"
	ActiveSound "knight/active"
	PainSound "knight/pain"
	DeathSound "knight/death"
	HitObituary "$OB_KNIGHTHIT"
	Obituary "$OB_KNIGHT"
	States
	{
	Spawn:
		BOS2 A 0
		Goto Super::Spawn+1
	// NORMAL DEATH STATES
	Death:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BOS2 H 0 A_NoBlocking
		BOS2 H 7 A_GiveToTarget("ScoreItem",300)
		BOS2 I 6 A_Scream
		BOS2 JKL 5
		goto CorpseFalling
	// COMEDY DEATH STATES
	BirthdayParty:
		TNT1 A 0 A_GiveToTarget("ScoreItem",300)
		Goto Super::BirthdayParty+1
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,60)
		//BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 A 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 A 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 B 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 B 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,60)
		BOS2 C 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 C 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 D 2 A_Wander
		BOS2 A 0 A_JumpIf(velz!=0,"MidAir")
		BOS2 D 2 A_Wander
		goto See
	}
}

ACTOR BOHBall : BaronBall replaces BaronBall
{
	SeeSound "imp/fire"
	damagetype Enemy
}