// Splot - Aliens
ACTOR Sectoid : MonsterTemplate
{
	Health 60
	Height 56
	Mass 100
	Speed 11
	PainChance 200
	PainSound "sectoid/pain"
	DeathSound "sectoid/die"
	Obituary "$OB_SECTOID"
	scale .75
	MaxTargetRange 1536	//Change this if it doesn't seem to work well...
	States
	{
	Spawn:
		GREY A 0
		GREY A 0 Thing_ChangeTID(0,900)
		GREY A 1 A_Look
		GREY A 0 A_AlertMonsters
		goto See
	See:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==9,"DogWhistle")
		//GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY A 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY A 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY C 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY C 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 4 A_Chase
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 4 A_Chase
		Loop
	MidAir:
		GREY BBBBBB 1 A_CheckFloor("See")
		TNT1 A 0 A_SpawnItemEx("CrushDamage")
		GREY B 1 A_CheckFloor("Landing")
		TNT1 A 0 A_JumpIf(velz==0,"See")
		goto MidAir+6
	Landing:
		GREY A 1
		GREY A 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		GREY A 5 A_PlaySound("human/land")
		goto See
	Melee:
	Missile:
		GREY DE 5 A_FaceTarget
		GREY F 5 A_CustomMissile("AlienBomb",1)
		GREY G 5
		Goto See
	Pain:
		GREY D 2
		GREY D 2 A_Pain
		Goto See
	// NORMAL DEATH STATES
	Death:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==4,"XDeath")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		GREY D 0 A_NoBlocking
		GREY D 5 A_GiveToTarget("ScoreItem",100)
		GREY H 4 A_Scream
		GREY IJ 3
		goto CorpseFalling
	CorpseFalling:
		GREY JKJKJK 1 A_CheckFloor("Corpse")
		GREY J 1 A_CheckFloor("CorpseHard")
		TNT1 A 0 A_JumpIf(velz==0,"Corpse")
		goto CorpseFalling+6
	Corpse:
		GREY L 1 A_PlaySound("human/body")
		GREY L 1 A_JumpIf(velz!=0,"CorpseFalling")
		TNT1 A 0 A_CheckSight("Cleanup")
		goto Corpse+1
	CorpseHard:
		GREY L 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto Corpse
	// GIB DEATH STATES
	XDeath:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		GREY N 0 A_NoBlocking
		GREY N 0 A_GiveToTarget("ScoreItem",130)
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		GREY N 0 A_PlaySound("human/gib")
		GREY N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GREY I 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		TNT1 A 0 A_SetScale(1)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	// COMEDY DEATH STATES
	Death.Explosive:
		BEGR F 0 A_Jump(64,"XDeath")
		BEGR F 0 A_GiveToTarget("ScoreItem",100)
		BEGR F 0 A_NoBlocking
		BEGR F 0 ThrustThingZ(0,random(32,128),0,1)
		BEGR F 0 A_Recoil(random(16,48))
		GREY H 2 A_Scream
		goto CorpseFlying
	CorpseFlying:
		GREY I 0 A_GiveToTarget("ScoreItem")
		BEGR G 0 A_SpawnItemEx("BloodShredSilent")
		BEGR G 0 A_SpawnItemEx("MissilePart",0,0,8)
		BEGR G 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		GREY I 1 A_CheckFloor("CorpseCrater")
		BEGR F 0 A_GiveToTarget("ScoreItem",1)
		BEGR H 0 A_SpawnItemEx("BloodShredSilent")
		BEGR H 0 A_SpawnItemEx("MissilePart",0,0,8)
		BEGR H 0 A_JumpIf(velx == 0||vely == 0,"CorpseCrater")
		GREY H 1 A_CheckFloor("CorpseCrater")
		loop
	CorpseCrater:
		BEGR M 0 A_SpawnItemEx("LandingPart",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		BEGR N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		BEGR N 0 A_PlaySound("human/gib")
		BEGR F 0 A_GiveToTarget("ScoreItem",50)
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		BEGR N 0 A_Stop
		goto Corpse+1
	Death.Fire:
		GREY A 0 A_PlaySound("human/scream")
		GREY A 0 A_SpawnItemEx("Flame1A",0,0,32)
		GREY A 0 A_Recoil(-8)
		GREY A 0 A_SpawnItemEx("Ember1",0,0,32)
		GREY AAAA 1 A_SetAngle(12 + angle)
		GREY B 0 A_Recoil(-8)
		GREY A 0 A_SpawnItemEx("Flame2A",0,0,32)
		GREY BBBB 1 A_SetAngle(12 + angle)
		GREY C 0 A_Recoil(-8)
		GREY A 0 A_SpawnItemEx("Flame1A",0,0,32)
		GREY CCCC 1 A_SetAngle(12 + angle)
		GREY D 0 A_Recoil(-8)
		GREY BBBB 1 A_SetAngle(12 + angle)
		GREY A 0 A_SpawnItemEx("Flame3B",0,0,32)
		GREY A 0 A_Jump(128,"Death.Fire")
		goto Death
	DogWhistle:
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		//GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY A 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY A 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		TNT1 A 0 A_SpawnItemEx("Confusion",0,0,56)
		GREY C 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 2 A_Wander
		GREY A 0 A_JumpIf(velz!=0,"MidAir")
		GREY B 2 A_Wander
		goto See
	Death.Barkblast:
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(901) ==8,"BirthdayParty")
		GREY N 0 A_NoBlocking
		GREY N 0 A_GiveToTarget("ScoreItem",50)
		GREY N 0 A_PlaySound("human/gib")
		GREY N 0 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GREY I 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		TNT1 A 0 A_SetScale(1)
		GIBS MNO 5 A_SpawnItemEx("HugeBloodShred",0,0,16)
		GIBS PQRST 4
		GIBS U 5
		GIBS V 1400
		Stop
	}
}

ACTOR AlienBomb
{
	+Missile
	+DROPOFF
	+FOILINVUL
	+RANDOMIZE
	+RIPPER
	+NOEXPLODEFLOOR
	+STEPMISSILE
	//+FLOORHUGGER
	MaxStepHeight 16
	Damage 1
	speed 16
	SeeSound "vile/firestrt"
	DamageType EnemyAlien
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAA 4 A_SpawnItemEx("AlienBombBoom")	//Lasts 72 tics; just over 2 seconds
		Stop
	Death:
		TNT1 A 0
		Stop
	}
}

ACTOR AlienBombBoom
{
	radius 11
	height 8
	+NOCLIP
	RenderStyle Add
	yscale 0.5
	+CLIENTSIDEONLY
	states
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_PlaySound("vile/firestrt")
	Doop:
		FIRE B 0 A_SetScale(1,scaleY + 0.5)
		FIRE B 2 BRIGHT A_FadeOut(0.1)
		loop
	}
}